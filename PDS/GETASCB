*+--------------------------------------------------------------------+    1
*|  FUNCTION:                                                         |    2
*|     GIVEN THE NAME OF AN ADDRESS SPACE (JOBNAME FOR BATCH JOB,     |    3
*|     PROC NAME FOR STARTED TASK OR LOGON ID FOR TSO ADDRESSS        |    4
*|     SPACE), RETURNS THE ADDRESS OF THE ASCB FOR THAT ADDRESS SPACE |    5
*|  INPUTS:                                                           |    6
*|     R1 - ADDRESS OF PARMLIST                                       |    7
*|          1ST PARM - LENGTH OF ADDRESS SPACE NAME    F              |    8
*|          2ST PARM - ADDRESS SPACE NAME              CL8            |    9
*|          3RD PARM - SEE OUTPUTS                     A              |   10
*|  OUTPUTS:                                                          |   11
*|     R15 - RETURN CODE                                              |   12
*|           00 - FOUND                                               |   13
*|           08 - NOT FOUND                                           |   14
*|     IF ADDRESS SPACE NAME FOUND, 3RD PARM IN PARMLIST              |   15
*|        (SEE INPUTS) CONTAINS ADDRESS OF ASCB                       |   16
*|  PROGRAM LOGIC:                                                    |   17
*|      1) LOCATE THE ADDRESS SPACE VECTOR TABLE (ASVT).              |   18
*|      2) LOOP THROUGH THE ASVT, COMPARING THE ADDRESS SPACE NAME    |   19
*|         FROM THE PARMLIST WITH THE NAME POINTED TO BY EACH ASCB.   |   20
*|      3) IF ADDRESS SPACE FOUND, PUT THE ASCB ADDRESS INTO PARMLIST.|   21
*|  ATTRIBUTES:                                                       |   22
*|      REENTERABLE, AMODE=31, RMODE=ANY                              |   23
*+--------------------------------------------------------------------+   24
*+--------------------------------------------------------------------+
*|  Copyright (c) 1992, 1993, 2002 by Mitchell Marx & Penelope Davis  |
*|                                                                    |
*|  Address questions to: mmarx@mdpaladin.com                         |
*|                                                                    |
*|  Permission is hereby granted, free of charge, to any person       |
*|  obtaining a copy of this software and associated documentation    |
*|  files (the "Software"), to deal in the Software without           |
*|  restriction, including without limitation the rights to use,      |
*|  copy, modify, merge, publish, distribute, sublicense, and/or sell |
*|  copies of the Software, and to permit persons to whom the         |
*|  Software is furnished to do so, subject to the following          |
*|  conditions:                                                       |
*|                                                                    |
*|  The above copyright notice and this permission notice shall be    |
*|  included in all copies or substantial portions of the Software.   |
*|                                                                    |
*|  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,   |
*|  EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES   |
*|  OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND          |
*|  NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT       |
*|  HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,      |
*|  WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING      |
*|  FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR     |
*|  OTHER DEALINGS IN THE SOFTWARE.                                   |
*+--------------------------------------------------------------------+
         MODULE GETASCB,BASE=12,AMODE=31,RMODE=ANY,                    X  25
               TEXT='SCAN ASVT FOR ADDRESS SPACE'                         26
*        *---------------------------------------*                        27
*        *     LOCATE THE ASVT                   *                        28
*        *---------------------------------------*                        29
         LR    R8,R1                   ADDRESS OF PARMLIST TO R8          30
         L     R4,CVTPTR               ADDRESS OF CVT TO REGISTER 4       31
         USING CVT,R4                  SET UP ADDRESSING FOR CVT          32
         L     R4,CVTASVT              ADDRESS OF ASVT                    33
         USING ASVT,R4                 SET UP ADDRESSING FOR ASVT         34
*        *---------------------------------------*                        35
*        *     LOOP THROUGH THE ASVT COMPARING   *                        36
*        *     ADDRESS SPACE NAME WITH PARMLIST  *                        37
*        *---------------------------------------*                        38
         LA    R5,ASVTFRST             ADDRESS OF FIRST ENTRY IN ASVT     39
         L     R6,ASVTMAXU             ASVT MAXIMUM ADDRESS SPACES        40
A$SVTLP1 DS    0H                                                         41
         TM    0(R5),ASVTAVAI          ADDRESS SPACE NUMBER UNUSED?       42
         BNO   A$CNT                   NO; CHECK THE ASCB                 43
         C     R5,=A(X'80000000')      IS THIS THE LAST ASVT ENTRY ?      44
         BE    A$NTFND                 YES; ASID NOT FOUND                45
         B     A$BMP                   NO; CHECK NEXT ASCB                46
A$CNT    DS    0H                                                         47
         L     R4,0(R5)                ADDRESS OF ASCB FROM ASVT          48
         USING ASCB,R4                 SET UP ADDRESSING FOR ASCB         49
         MVC   PGM(L'PGM),SPACES       INITIALIZE FORMAT AREA             50
         L     R9,0(R8)                LENGTH OF ADDRESS SPACE NAME       51
         BCTR  R9,0                    LESS 1 FOR EXECUTE                 52
         EX    R9,A$MVIT               MOVE TO FORMAT AREA                53
         B     A$MVITA                                                    54
A$MVIT   MVC   PGM(0),4(R8)            ** EXECUTE ONLY **                 55
A$MVITA  DS    0H                                                         56
         L     R7,ASCBJBNS             ASCB POINTER TO JOBNAME FIELD      57
         CLC   0(L'INIT,R7),INIT       IS THIS AN INITIATOR ?             58
         BNE   A$NTINIT                NO; USE ASCBJBNS FOR COMPARE       59
         ICM   R7,15,ASCBJBNI          ADDRESS OF JOBNAME FIELD IN INIT   60
         BZ    A$BMP                   =0; UNUSED INITIATOR               61
A$NTINIT DS    0H                                                         62
         CLC   0(L'PGM,R7),PGM         IS THIS THE ADDRESS SPACE ?        63
         BE    A$YFND                  YES; ASCB ADDRESS TO PARMLIST      64
A$BMP    DS    0H                                                         65
         LA    R5,L'ASVTENTY(R5)       BUMP TO NEXT ENTRY                 66
         BCT   R6,A$SVTLP1             CHECK NEXT ASVT ENTRY              67
A$NTFND  DS    0H                                                         68
         B     EXIT08                  ADDRESS SPACE NOT FOUND            69
A$YFND   DS    0H                                                         70
*        *---------------------------------------*                        71
*        *     PUT ASCB ADDRESS IN PARMLIST      *                        72
*        *---------------------------------------*                        73
         ST    R4,12(R8)               PUT ASCB ADDRESS IN PARMLIST       74
         B     EXIT00                  ADDRESS SPACE FOUND                75
*---------------------------------------------------------------------*   76
*        EXIT ROUTINES                                                *   77
*---------------------------------------------------------------------*   78
EXIT00   DS    0H                      ADDRESS SPACE FOUND                79
         LA    R15,X'00'                                                  80
         B     EXIT                                                       81
EXIT08   DS    0H                      ADDRESS SPACE NOT FOUND            82
         LA    R15,X'08'                                                  83
         B     EXIT                                                       84
*---------------------------------------------------------------------*   85
EXIT     DS   0H                                                          86
         ENDMOD                                                           87
*---------------------------------------------------------------------*   88
*        CONSTANTS                                                    *   89
*---------------------------------------------------------------------*   90
SPACES   DC    CL8' '                  8 BYTES OF X'40'                   91
INIT     DC    CL8'INIT'               ADDRESS SPACE NAME FOR INITIATOR   92
*---------------------------------------------------------------------*   93
*        WORK AREA                                                    *   94
*---------------------------------------------------------------------*   95
WGETASCB DSECT                                                            96
PGM      DS    CL8                     FORMAT AREA-ADDRESS SPACE NAME     97
LGETASCB EQU   *-WGETASCB                                                 98
*---------------------------------------------------------------------*   99
*        OTHER DSECTS                                                 *  100
*---------------------------------------------------------------------*  101
         PRINT NOGEN                                                     102
         CVT     DSECT=YES,LIST=NO      MAP CVT                          103
         IHAASVT DSECT=YES              MAP ASVT                         104
         IHAASCB DSECT=YES              MAP ASCB                         105
         END                                                             106
