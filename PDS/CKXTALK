*+--------------------------------------------------------------------+    1
*|  FUNCTION:                                                         |    2
*|     CKXTALK IS A SPACE-SWITCHING PC ROUTINE THAT INVOKES XTALK     |    3
*|     TO BUILD AN ACCESS LIST ENTRY ON THE DU-AL ASSOCIATED WITH     |    4
*|     THE CALLING PROGRAM'S TCB.  BEFORE BUILDING THE ACCESS LIST    |    5
*|     ENTRY, CKXTALK CHECKS A TABLE TO DETERMINE IF THE CALLER IS    |    6
*|     AUTHORIZED TO ESTABLISH COMMUNICATION WITH THE TARGET SPACE.   |    7
*|  LOGIC:                                                            |    8
*|    1) GET THE CALLER'S PARAMETER LIST BY LOADING 1 INTO ACCESS     |    9
*|       REGISTER 1, GOING INTO AR MODE, AND MOVING THE PARMLIST      |   10
*|       FROM THE CALLER'S ADDRESS SPACE TO THIS ADDRESS SPACE.       |   11
*|       THE ETDEF THAT DEFINED CKXTALK MUST CONTAIN SASN=OLD.        |   12
*|    2) GET THE NAME OF THE CALLER'S ADDRESS SPACE BY USING          |   13
*|       THE SASN AS AN INDEX INTO THE ASVT.                          |   14
*|    3) SEARCH THE PERMISSIONS TABLE WHOSE ADDRESS WAS PASSED        |   15
*|       AS A LATENT PARAMETER BY THE CALLER.  IF THE CALLING         |   16
*|       ADDRESS SPACE NAME AND THE TARGET ADDRESS SPACE NAME         |   17
*|       ARE BOTH FOUND IN AN ENTRY IN THE PERMISSIONS TABLE,         |   18
*|       CONTINUE.  ELSE EXIT.                                        |   19
*|    4) CALL THE XTALK ROUTINE TO BUILD THE ACCESS LIST ENTRY.       |   20
*|  INPUTS:                                                           |   21
*|     GR1 - ADDRESS OF CALLER'S PARMLIST                             |   22
*|           THIS PARMLIST HAS THE SAME FORMAT AS THE XTALK PARMLIST. |   23
*|     GR4 - ADDRESS OF LATENT PARAMETER LIST                         |   24
*|           1ST WORD - ADDRESS OF PERMISSIONS TABLE                  |   25
*|     SASN  IS CALLER'S PASN.                                        |   26
*|  OUTPUTS:                                                          |   27
*|    GR15 - RETURN CODE                                              |   28
*|           0  - ACCESS LIST ENTRY IS BUILT                          |   29
*|           8  - CALLER IS NOT AUTHORIZED TO BUILD ACCESS LIST ENTRY |   30
*|                TO TARGET.                                          |   31
*|          12  - XTALK ROUTINE IS NOT LINK-EDITED.                   |   32
*|          16  - XTALK ROUTINE FAILED.                               |   33
*|                R0 - CONTAINS RETURN CODE FROM XTALK.               |   34
*|                R1 - CONTAINS REASON CODE FROM XTALK.               |   35
*|  ATTRIBUTES:                                                       |   36
*|    AMODE 31, RMODE ANY, REENTRANT                                  |   37
*+--------------------------------------------------------------------+   38
*+--------------------------------------------------------------------+
*|  Copyright (c) 1992, 1993, 2002 by Mitchell Marx & Penelope Davis  |
*|                                                                    |
*|  Address questions to: mmarx@mdpaladin.com                         |
*|                                                                    |
*|  Permission is hereby granted, free of charge, to any person       |
*|  obtaining a copy of this software and associated documentation    |
*|  files (the "Software"), to deal in the Software without           |
*|  restriction, including without limitation the rights to use,      |
*|  copy, modify, merge, publish, distribute, sublicense, and/or sell |
*|  copies of the Software, and to permit persons to whom the         |
*|  Software is furnished to do so, subject to the following          |
*|  conditions:                                                       |
*|                                                                    |
*|  The above copyright notice and this permission notice shall be    |
*|  included in all copies or substantial portions of the Software.   |
*|                                                                    |
*|  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,   |
*|  EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES   |
*|  OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND          |
*|  NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT       |
*|  HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,      |
*|  WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING      |
*|  FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR     |
*|  OTHER DEALINGS IN THE SOFTWARE.                                   |
*+--------------------------------------------------------------------+
         MODULE CKXTALK,BASE=12,AMODE=31,RMODE=ANY,ENTRY=PCSTACK,      X  39
               TEXT='PC RTN TO BUILD ACCESS LIST ENTRY'                   40
*        *---------------------------------------*                        41
*        *     GET THE NAME OF TARGET ADDRESS    *                        42
*        *     SPACE FROM CALLER'S PARMLIST.     *                        43
*        *---------------------------------------*                        44
         LAM   A1,A1,=A(X'00000001')   SECONDARY ADDRESS SPACE            45
         SAC   512                     GO INTO ACCESS REGISTER MODE.      46
         MVC   XTPARMS(XMSESAL),0(R1)  MOVE PARMLIST FROM CALLER'S SPC    47
         LAE   R3,XTPARMS              ADDRESS OF PARMLIST FOR XTALK      48
         USING XMSESA,R3               MAP PARMLIST                       49
         SAC   0                       SET PRIMARY SPACE MODE             50
*        *---------------------------------------*                        51
*        *     GET THE NAME OF CALLER'S          *                        52
*        *     ADDRESS SPACE.                    *                        53
*        *---------------------------------------*                        54
         L     R5,CVTPTR               ADDRESS OF CVT                     55
         L     R5,CVTASVT-CVT(R5)      ADDRESS OF ASVT                    56
         ESAR  R6                      EXTRACT SECONDARY ASID             57
         SLL   R6,2                    MULTIPLY BY 4                      58
         L     R5,ASVTFRST-ASVT(R5,R6)  LOAD ADDRESS OF CALLER'S ASCB     59
         USING ASCB,R5                 MAP THE ASCB                       60
         L     R7,ASCBJBNS             POINTER TO JOBNAME FIELD           61
         CLC   0(L'INIT,R7),INIT       IS IT AN INITIATOR ?               62
         BNE   NOTINIT                 NO; USE ASCBJBNS                   63
         L     R7,ASCBJBNI             ADDRESS OF JOBNAME FOR INIT        64
         MVC   CALLER(L'CALLER),0(R7)  MOVE CALLER'S NAME TO WORKAREA     65
NOTINIT  DS    0H                                                         66
*        *---------------------------------------*                        67
*        *     SEARCH PERMIT TABLE               *                        68
*        *---------------------------------------*                        69
*                                      .. ADDRESS OF PERMISSIONS TABLE    70
         L     R7,0(R4)                .. IS FIRST LATENT PARM            71
TLOOP    DS    0H                                                         72
         CLC   0(L'CALLER,R7),ENDTAB   END OF PERMISSIONS TABLE ?         73
         BE    EXIT08                  YES; MATCH NOT FOUND               74
         CLC   0(L'CALLER,R7),CALLER   IS THIS THE RIGHT CALLER ?         75
         BNE   NEXTENT                 NO; CHECK NEXT ENTRY               76
         CLC   8(L'XMNAME,R7),XMNAME   IS THIS THE RIGHT TARGET ?         77
         BE    FOUND                   YES; CALL XTALK                    78
NEXTENT  LA    R7,16(R7)               POINT AT NEXT ENTRY                79
         B     TLOOP                   CHECK NEXT PERMIT TABLE ENTRY      80
*        *---------------------------------------*                        81
*        *     CALL XTALK                        *                        82
*        *---------------------------------------*                        83
FOUND    DS    0H                                                         84
         LA    R7,8                    LENGTH OF ADDRESS SP NAME          85
         ST    R7,XMNAMEL              PUT IN XTALK PARMLIST              86
         LAE   R1,0(0,R3)              PARMLIST ADDRESS                   87
         ICM   R15,15,=V(XTALK)        ADDRESS OF XTALK ROUTINE           88
         BZ    EXIT0C                  NOT FOUND; EXIT                    89
         O     R15,=A(X'80000000')     SET 31 BIT ENTRY                   90
         BASSM R14,R15                 INVOKE THE ROUTINE                 91
         LTR   R15,R15                 RC = 0 ?                           92
         BNZ   EXIT10                  NO; EXIT RC = 16                   93
         EREG  R1,R1                   EXTRACT FROM STACK PARMLIST        94
         LAM   A1,A1,=A(X'00000001')   LOAD R1 WITH SECONDARY ADD/SP      95
         SAC   512                     GO INTO ACCESS REGISTER MODE       96
         MVC   0(XMSESAL,R1),XTPARMS   MOVE PARMLIST TO CALLERS ADD.SP    97
         SAC   0                       GO INTO PRIMARY SPACE MODE         98
         B     EXIT00                                                     99
*---------------------------------------------------------------------*  100
*        EXIT ROUTINES                                                *  101
*---------------------------------------------------------------------*  102
EXIT00   DS    0H                      SUCCESSFUL COMPLETION             103
         LA    R15,X'00'                                                 104
         B     EXIT                                                      105
EXIT08   DS    0H                      PERMISSION NOT ALLOWED            106
         LA    R15,X'08'                                                 107
         B     EXIT                                                      108
EXIT0C   DS    0H                      XTALK ROUTINE NOT LINKED          109
         LA    15,X'0C'                                                  110
         B     EXIT                                                      111
EXIT10   DS    0H                      XTALK SERVICE FAILED              112
         PERCRC RC=X'10'                                                 113
         B     EXIT                                                      114
*---------------------------------------------------------------------*  115
*        COMMON EXIT                                                  *  116
*---------------------------------------------------------------------*  117
EXIT     DS   0H                                                         118
         ENDMOD                                                          119
*---------------------------------------------------------------------*  120
*        CONSTANTS                                                    *  121
*---------------------------------------------------------------------*  122
INIT     DC    CL8'INIT'               TO TEST INITIATOR ASCB NAMES      123
ENDTAB   DC    8X'FF'                  END OF PERMISSIONS TABLE LITERAL  124
*---------------------------------------------------------------------*  125
*        WORK AREA                                                    *  126
*---------------------------------------------------------------------*  127
WCKXTALK DSECT                                                           128
XTPARMS  DS    0F,CL(XMSESAL)          XTALK PARMLIST                    129
CALLER   DS    CL8                     XMNAME ADDRESS SPACE              130
LCKXTALK EQU   *-WCKXTALK                                                131
*---------------------------------------------------------------------*  132
*        OTHER DSECTS                                                 *  133
*---------------------------------------------------------------------*  134
         XMSESA                        MAP XTALK PARMLIST                135
         PCSERV                        MAP PC SERVICES ANCHOR BLOCK      136
         CVT     DSECT=YES             MAP CVT                           137
         IHAASVT DSECT=YES             MAP ASVT                          138
         IHAASCB DSECT=YES             MAP ASCB                          139
         END                                                             140
