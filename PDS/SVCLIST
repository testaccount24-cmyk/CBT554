*+--------------------------------------------------------------------+    1
*|   FUNCTION:                                                        |    2
*|      PRINT SVC ROUTINE ADDRESSES AND ATTRIBUTES.                   |    3
*|   INPUTS:                                                          |    4
*|      NONE                                                          |    5
*|   OUTPUTS:                                                         |    6
*|      PRINTED LIST OF SVC TO FILE WITH DDNAME=PRINT                 |    7
*|   PROGRAM LOGIC:                                                   |    8
*|      1) SET UP REENTRANT QSAM I/O; OPEN OUTPUT FILE.               |    9
*|      2) USE NUCLKUP TO FIND ADDRESS OF IGCERROR (UNUSED SVC ENTRY).|   10
*|      3) LOCATE THE SVCTABLE.                                       |   11
*|      4) LOOP THROUGH SVC TABLE ENTRIES, PRINTING ENTRY POINTS      |   12
*|         AND ROUTINE ATTRIBUTES.                                    |   13
*|      5) PRINT THE ADDRESS OF IGCERROR.                             |   14
*|      6) CLOSE OUTPUT FILE.                                         |   15
*|   MODULES CALLED:                                                  |   16
*|      NONE                                                          |   17
*|   ATTRIBUTES:                                                      |   18
*|      AMODE 31, RMODE 24, REENTRANT, USES ESA-ONLY INSTRUCTIONS.    |   19
*+--------------------------------------------------------------------+   20
*+--------------------------------------------------------------------+
*|  Copyright (c) 1992, 1993, 2002 by Mitchell Marx & Penelope Davis  |
*|                                                                    |
*|  Address questions to: mmarx@mdpaladin.com                         |
*|                                                                    |
*|  Permission is hereby granted, free of charge, to any person       |
*|  obtaining a copy of this software and associated documentation    |
*|  files (the "Software"), to deal in the Software without           |
*|  restriction, including without limitation the rights to use,      |
*|  copy, modify, merge, publish, distribute, sublicense, and/or sell |
*|  copies of the Software, and to permit persons to whom the         |
*|  Software is furnished to do so, subject to the following          |
*|  conditions:                                                       |
*|                                                                    |
*|  The above copyright notice and this permission notice shall be    |
*|  included in all copies or substantial portions of the Software.   |
*|                                                                    |
*|  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,   |
*|  EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES   |
*|  OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND          |
*|  NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT       |
*|  HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,      |
*|  WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING      |
*|  FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR     |
*|  OTHER DEALINGS IN THE SOFTWARE.                                   |
*+--------------------------------------------------------------------+
         MODULE SVCLIST,BASE=12,LOC=BELOW,AMODE=31,RMODE=24,           X  21
               TEXT='PRINT SVC TABLE'                                     22
*        *---------------------------------------*                        23
*        *     SET UP OUTPUT FILE AND OPEN       *                        24
*        *---------------------------------------*                        25
         MVC   PRINT(LPRINT),MPRINT    COPY DCB TO REENTRANT AREA         26
         MVC   OLIST(LOLIST),MOLIST    COPY OPEN LIST TO REENTRANT AREA   27
         OPEN  (PRINT,OUTPUT),MF=(E,OLIST)      OPEN PRINT FILE           28
         MVI   OUTLINE,X'40'                    ..INITIALIZE PRINT LINE   29
         MVC   OUTLINE+1(L'OUTLINE-1),OUTLINE   ..TO SPACES               30
         ZAP   SVCCNTR,=P'0'           INITIALIZE SVC COUNTER             31
         ZAP   LINECNT,=P'99'          INITIALIZE LINE COUNTER            32
*        *---------------------------------------*                        33
*        *     GET ADDRESS OF UNUSED SVC         *                        34
*        *     ENTRY POINT IN MVS NUCLEUS        *                        35
*        *---------------------------------------*                        36
         NUCLKUP BYNAME,NAME=NUCMOD,ADDR=(0)                              37
         N     R0,HIGHOFF              TURN OFF HIGH ORDER BIT            38
         ST    R0,NULL                 SAVE THE ADDRESS                   39
*        *---------------------------------------*                        40
*        *     LOCATE THE SVC TABLE              *                        41
*        *---------------------------------------*                        42
         L     R3,CVTPTR               ADDRESS OF THE CVT                 43
         L     R3,CVTABEND-CVT(R3)     ADDRESS OF THE SCVT                44
         L     R3,SCVTSVCT-SCVTSECT(R3)  ADDRESS OF THE SVC TABLE         45
         USING SVC,R3                  MAP THE SVC TABLE                  46
*---------------------------------------------------------------------*   47
*        LOOP THRU SVC TABLE FORMATTING AND PRINTING                  *   48
*---------------------------------------------------------------------*   49
         LA    R4,256                  NUMBER SVCTABLE ENTRIES            50
MAINLOOP DS    0H                                                         51
         CP    LINECNT,PAGEMAX         END OF PAGE ?                      52
         BL    NOHEAD                  NO; CONTINUE                       53
         BAS   R14,HEADRTN             CALL HEADING RTN                   54
         ZAP   LINECNT,=P'0'           ZERO LINE COUNTER                  55
NOHEAD   DS    0H                                                         56
         ICM   R1,B'1111',SVCEP        ENTRY POINT FROM TABLE             57
         N     R1,HIGHOFF              TURN OFF HIGH ORDER BIT            58
         CL    R1,NULL                 NO SVC ?                           59
         BNE   ISOKSVC                 NO; PROCESS TABLE ENTRY            60
*                                      MOVE "UNUSED" TO ADDRESS           61
         MVC   OUTADDR+1(L'UNUSED),UNUSED                                 62
         B     CKTYPE                  ADD REST OF ENTRY                  63
ISOKSVC  DS    0H                                                         64
         ICM   R0,15,SVCEP             ROUTINE ADDRESS IN TABLE           65
         LA    R1,OUTADDR              ADDRESS OF PRINTING AREA           66
         BAS   R14,HEXOUT              CONVERT TO PRINTING CHARS          67
CKTYPE   DS    0H                                                         68
*        *---------------------------------------*                        69
*        *     DETERMINE SVC TYPE                *                        70
*        *---------------------------------------*                        71
         MVC   TESTBYTE,SVCTYP         TYPE BITS TO WORK AREA             72
         NI    TESTBYTE,X'F0'          MASK OFF ALL BUT TYPE BITS         73
         CLI   TESTBYTE,SVCTP1         TYPE1 ?                            74
         BNE   NTYP1                   NO; TEST OTHER TYPES               75
         MVI   OUTTYPE+1,C'1'          MOVE TYPE TO OUTPUT LINE           76
         B     ENDTYPE                 CHECK OTHER ATTRIBUTES             77
NTYP1    DS    0H                                                         78
         CLI   TESTBYTE,SVCTP2         TYPE2 ?                            79
         BNE   NTYP2                   NO; TEST OTHER TYPES               80
         MVI   OUTTYPE+1,C'2'          MOVE TYPE TO OUTPUT LINE           81
         B     ENDTYPE                 CHECK OTHER ATTRIBUTES             82
NTYP2    DS    0H                                                         83
         CLI   TESTBYTE,SVCTP34        TYPE3 OR TYPE 4 ?                  84
         BNE   NTP34                   NO; TEST OTHER TYPES               85
         MVC   OUTTYPE+1(L'TP34),TP34  MOVE TYPE TO OUTPUT LINE           86
         B     ENDTYPE                 CHECK OTHER ATTRIBUTES             87
NTP34    DS    0H                                                         88
         CLI   TESTBYTE,SVCTP6         TYPE6 ?                            89
         BNE   NTYP6                   NO; TEST OTHER TYPES               90
         MVI   OUTTYPE+1,C'6'          MOVE TYPE TO OUTPUT LINE           91
         B     ENDTYPE                 CHECK OTHER ATTRIBUTES             92
NTYP6    DS    0H                                                         93
         MVI   OUTTYPE+1,C'U'          UNKNOWN TYPE                       94
ENDTYPE  DS    0H                                                         95
*        *---------------------------------------*                        96
*        *     DETERMINE OTHER ATTRIBUTES        *                        97
*        *---------------------------------------*                        98
         TM    SVCTYP,SVCAPF           APF AUTHORIZATION REQUIRED ?       99
         BZ    NAPF                    NO; DO NOT INDICATE               100
         MVI   OUTAPF+1,C'X'           INDICATE                          101
NAPF     DS    0H                                                        102
         TM    SVCTYP,SVCESR           ESR ?                             103
         BZ    NESR                    NO; DO NOT INDICATE               104
         MVI   OUTESR+1,C'X'           INDICATE                          105
NESR     DS    0H                                                        106
         TM    SVCTYP,SVCNP            NON PRE-EMPTIVE SVC ?             107
         BZ    NNP                     NO; DO NOT INDICATE               108
         MVI   OUTNP+1,C'X'            INDICATE                          109
NNP      DS    0H                                                        110
         TM    SVCTYP,SVCASF           CAN BE ASSISTED ?                 111
         BZ    NASF                    NO; DO NOT INDICATE               112
         MVI   OUTASF+1,C'X'           INDICATE                          113
NASF     DS    0H                                                        114
*        *---------------------------------------*                       115
*        *     DETERMINE OBTAINED LOCKS          *                       116
*        *---------------------------------------*                       117
         TM    SVCLOCKS,SVCLL          LOCAL LOCK ?                      118
         BZ    NLL                     NO; DO NOT INDICATE               119
         MVI   OUTLL+1,C'X'            INDICATE                          120
NLL      DS    0H                                                        121
         TM    SVCLOCKS,SVCCMS         CMS LOCK ?                        122
         BZ    NCMS                    NO; DO NOT INDICATE               123
         MVI   OUTCMS+1,C'X'           INDICATE                          124
NCMS     DS    0H                                                        125
         TM    SVCLOCKS,SVCOPT         OPT LOCK ?                        126
         BZ    NOPT                    NO; DO NOT INDICATE               127
         MVI   OUTOPT+1,C'X'           INDICATE                          128
NOPT     DS    0H                                                        129
         TM    SVCLOCKS,SVCALLOC       ALLOC LOCK ?                      130
         BZ    NALLOC                  NO; DO NOT INDICATE               131
         MVI   OUTALLOC+1,C'X'         INDICATE                          132
NALLOC   DS    0H                                                        133
         TM    SVCLOCKS,SVCDISP        DISP LOCK ?                       134
         BZ    NDISP                   NO; DO NOT INDICATE               135
         MVI   OUTDISP+1,C'X'          INDICATE                          136
NDISP    DS    0H                                                        137
         MVC   OUTCNT(L'PATTERN),PATTERN  ..MOVE EDIT PATTERN TO         138
         ED    OUTCNT(L'PATTERN),SVCCNTR  ..OUTPUT AND EDIT SVC NUMBER   139
         BAS   R14,PRINTIT             PRINT THE LINE                    140
         LA    R3,SVCLENT(R3)          POINT AT NEXT SVCTABLE ENTRY      141
         AP    SVCCNTR,=P'1'           ADD 1 TO SVC NUMBER               142
         AP    LINECNT,=P'1'           ADD 1 TO LINE COUNTER             143
         BCT   R4,MAINLOOP             PROCESS NEXT SVC TABLE ENTRY      144
*---------------------------------------------------------------------*  145
*        END OF LOOP THROUGH SVC TABLE ENTRIES                        *  146
*---------------------------------------------------------------------*  147
*        *---------------------------------------*                       148
*        *     PRINT ADDRESS OF IGCERROR         *                       149
*        *---------------------------------------*                       150
         MVI   CONTROL,TRIPLE          TRIPLE SPACE                      151
         MVC   OUTADDR(L'IGCER),IGCER  MOVE LITERAL TO OUTPUT LINE       152
         ICM   R0,15,NULL              ADDRESS OF IGCERROR               153
         LA    R1,OUTADDR+L'IGCER      ADDRESS OF PRINTING AREA          154
         BAS   R14,HEXOUT              MAKE INTO DISPLAY FORMAT          155
         BAS   R14,PRINTIT             PRINT THE LINE                    156
*        *---------------------------------------*                       157
*        *     CLOSE PRINT FILE                  *                       158
*        *---------------------------------------*                       159
         CLOSE ,MF=(E,OLIST)           CLOSE THE FILE                    160
         B     EXIT00                                                    161
*---------------------------------------------------------------------*  162
*        HEXOUT - CONVERT HEX FULLWORD TO 8 BYTES CHARACTER HEX       *  163
*                 INPUT:  R0 - 4 BYTES OF HEX                         *  164
*                         R1 - ADDRESS OF 8 BYTE OUTPUT FIELD         *  165
*                 OUTPUT: CHARACTER FORMAT AT ADDRESS IN R1           *  166
*---------------------------------------------------------------------*  167
HEXOUT   DS    0H                                                        168
         BAKR  R14,0                   PUSH ENVIRNONMENT ONTO STACK      169
         LA    R1,8(R1)                ADDRESS LAST BYTE + 1 OF OUTPUT   170
         LA    R2,8                    LENGTH OF OUTPUT FIELD            171
ADDRLOOP DS    0H                                                        172
         BCTR  R1,0                    POINT AT PREVIOUS BYTE            173
         STC   R0,0(R1)                LAST BYTE OF INPUT FIELD          174
         NI    0(R1),X'0F'             MASK OFF HIGH ORDER BITS          175
         SRL   R0,4                    SHIFT OFF LOW ORDER BITS          176
         BCT   R2,ADDRLOOP             BRANCH TO PUT IN OUTPUT           177
         TR    0(8,R1),=C'0123456789ABCDEF'   TRANSLATE OUTPUT           178
         PR                            POP STACK AND RETURN TO CALLER    179
*---------------------------------------------------------------------*  180
*        HEADRTN - PRINT HEADINGS                                     *  181
*---------------------------------------------------------------------*  182
HEADRTN  DS    0H                                                        183
         BAKR  R14,0                   PUSH ENVIRNONMENT ONTO STACK      184
         MVI   CONTROL,NEWPAGE         SET NEW PAGE                      185
         MVC   OUTLINE+55(L'HD1),HD1   MOVE PAGE HEADING                 186
         BAS   R14,PRINTIT             PRINT PAGE HEADING                187
         MVI   CONTROL,DOUBLE          DOUBLE SPACE                      188
         MVC   OUTCNT(L'LCNT),LCNT        ... MOVE COLUMN HEADINGS       189
         MVC   OUTADDR(L'LADDR),LADDR     ...                            190
         MVC   OUTTYPE(L'LTYPE),LTYPE     ...                            191
         MVC   OUTAPF(L'LAPF),LAPF        ...                            192
         MVC   OUTESR(L'LESR),LESR        ...                            193
         MVC   OUTNP(L'LNP),LNP           ...                            194
         MVC   OUTASF(L'LASF),LASF        ...                            195
         MVC   OUTLL(L'LLL),LLL           ...                            196
         MVC   OUTCMS(L'LCMS),LCMS        ...                            197
         MVC   OUTOPT(L'LOPT),LOPT        ...                            198
         MVC   OUTALLOC(L'LALLOC),LALLOC  ...                            199
         MVC   OUTDISP(L'LDISP),LDISP     ... MOVE COLUMN HEADINGS       200
         BAS   R14,PRINTIT              PRINT COLUMN HEADINGS            201
         MVC   OUTCNT(L'OUTCNT),DASH      ... MOVE DASHES OVER COLUMN    202
         MVC   OUTADDR(L'OUTADDR),DASH    ...                            203
         MVC   OUTTYPE(L'OUTTYPE),DASH    ...                            204
         MVC   OUTAPF(L'OUTAPF),DASH      ...                            205
         MVC   OUTESR(L'OUTESR),DASH      ...                            206
         MVC   OUTNP(L'OUTNP),DASH        ...                            207
         MVC   OUTASF(L'OUTASF),DASH      ...                            208
         MVC   OUTLL(L'OUTLL),DASH        ...                            209
         MVC   OUTCMS(L'OUTCMS),DASH      ...                            210
         MVC   OUTOPT(L'OUTOPT),DASH      ...                            211
         MVC   OUTALLOC(L'OUTALLOC),DASH  ...                            212
         MVC   OUTDISP(L'OUTDISP),DASH    ... MOVE DASHES OVER COLUMN    213
         BAS   R14,PRINTIT             PRINT DASHES                      214
         MVI   CONTROL,DOUBLE          SET DOUBLE SPACE                  215
         PR                            POP STACK AND RETURN TO CALLER    216
*---------------------------------------------------------------------*  217
*        PRINTIT - PRINT AN OUTPUT LINE                               *  218
*---------------------------------------------------------------------*  219
PRINTIT  DS    0H                                                        220
         BAKR  R14,0                   PUSH ENVIRNONMENT ONTO STACK      221
         PUT24 PRINT,OUTLINE,REG=3     PRINT THE OUTPUT LINE             222
         MVI   OUTLINE,X'40'                    ..INITIALIZE PRINT LINE  223
         MVC   OUTLINE+1(L'OUTLINE-1),OUTLINE   ..TO SPACES              224
         PR                            POP STACK AND RETURN TO CALLER    225
*---------------------------------------------------------------------*  226
*        EXIT ROUTINES                                                *  227
*---------------------------------------------------------------------*  228
EXIT00   DS    0H                      SUCCESSFUL COMPLETION             229
         LA    R15,X'00'                                                 230
         B     EXIT                                                      231
*---------------------------------------------------------------------*  232
*        COMMON EXIT                                                  *  233
*---------------------------------------------------------------------*  234
EXIT     DS   0H                                                         235
         ENDMOD                                                          236
*---------------------------------------------------------------------*  237
*        CONSTANTS                                                    *  238
*---------------------------------------------------------------------*  239
HIGHOFF  DC    A(X'7FFFFFFF')          TURN OFF HIGH ORDER BIT  MASK     240
NUCMOD   DC    CL8'IGCERROR'           MODULE NAME FOR NUCLEUS LOOKUP    241
UNUSED   DC    C'UNUSED'               LITERAL FOR UNUSED SVC            242
PAGEMAX  DC    PL2'50'                 MAXIMUM LINES/PAGE                243
PATTERN  DC    X'40202120'             EDIT PATTERN FOR SVC NUMBER       244
HD1      DC    C'DUMP OF SVC TABLE'    HEADING                           245
DASH     DC    C'----------------------------'                           246
LCNT     DC    C'SVC #'                ...FIELDS USED IN PAGE HEADERS    247
LADDR    DC    C'ADDRESS'              ...                               248
LTYPE    DC    C'TYPE'                 ...                               249
LAPF     DC    C'APF'                  ...                               250
LESR     DC    C'ESR'                  ...                               251
LNP      DC    C'NP'                   ...                               252
LASF     DC    C'ASF'                  ...                               253
LLL      DC    C'LCL'                  ...                               254
LCMS     DC    C'CMS'                  ...                               255
LOPT     DC    C'OPT'                  ...                               256
LALLOC   DC    C'ALLC'                 ...                               257
LDISP    DC    C'DISP'                 ...                               258
TP34     DC    C'3 OR 4'               TYPE 3 OR 4 LITERAL               259
IGCER    DC    C'ADDRESS OF IGCERROR (UNUSED SVC) = '                    260
*---------------------------------------------------------------------*  261
*        DCB AND OPEN/CLOSE PARAMETER LIST MODELS                     *  262
*---------------------------------------------------------------------*  263
         PRINT NOGEN                                                     264
MPRINT   DCB   DDNAME=PRINT,DSORG=PS,LRECL=133,RECFM=FBA,MACRF=PM        265
LPRINT   EQU   *-MPRINT                LENGTH OF THE DCB                 266
         PRINT GEN                                                       267
MOLIST   OPEN  (,),MF=L                OPEN/CLOSE PARMLIST               268
LOLIST   EQU   *-MOLIST                LENGTH OF OPEN/CLOSE PARMLIST     269
*---------------------------------------------------------------------*  270
*        WORK AREA                                                    *  271
*---------------------------------------------------------------------*  272
WSVCLIST DSECT                                                           273
NULL     DS    F                       A(UNUSED SVC)                     274
PRINT    DS    CL(LPRINT)              PRINT                             275
OLIST    DS    CL(LOLIST)              LIST FOR OPEN                     276
TESTBYTE DS    X                       WORK BYTE                         277
SVCCNTR  DS    PL2                     SVC NUMBER COUNTER                278
LINECNT  DS    PL2                     LINE COUNTER                      279
OUTLINE  DS    CL133                   OUTPUT LINE                       280
CONTROL  EQU   OUTLINE,1               PRINT CONTROL CHARACTER           281
NEWPAGE  EQU   C'1'                    ANSI PRINT CNTL - NEW PAGE        282
SINGLE   EQU   X'40'                   ANSI PRINT CNTL - SINGLE SPACE    283
DOUBLE   EQU   C'0'                    ANSI PRINT CNTL - DOUBLE SPACE    284
TRIPLE   EQU   C'-'                    ANSI PRINT CNTL - TRIPLE SPACE    285
*        *---------------------------------------*                       286
*        *     PRINT LINE FORMAT                 *                       287
*        *---------------------------------------*                       288
OUTCNT   EQU   OUTLINE+7,8             SVC NUMBER                        289
OUTADDR  EQU   OUTLINE+20,8            ADDRESS SVC ROUTINE               290
OUTTYPE  EQU   OUTLINE+30,8            TYPE SVC ROUTINE                  291
OUTAPF   EQU   OUTLINE+43,4            ...ATTRIBUTES OF SVC ROUTINE      292
OUTESR   EQU   OUTLINE+48,4            ...                               293
OUTNP    EQU   OUTLINE+53,4            ...                               294
OUTASF   EQU   OUTLINE+58,4            ...                               295
OUTLL    EQU   OUTLINE+73,4            ...                               296
OUTCMS   EQU   OUTLINE+78,4            ...                               297
OUTOPT   EQU   OUTLINE+83,4            ...                               298
OUTALLOC EQU   OUTLINE+88,4            ...                               299
OUTDISP  EQU   OUTLINE+93,4            ...                               300
LSVCLIST EQU   *-WSVCLIST              LENGTH OF THE WORK AREA           301
*---------------------------------------------------------------------*  302
*        MAP SVCTABLE ENTRY                                           *  303
*---------------------------------------------------------------------*  304
SVC      DSECT                                                           305
SVCENTRY EQU   *                                                         306
SVCEP    DS    AL4                     ENTRY POINT OF SVC ROUTINE        307
SVCATTR1 DS    0XL2                    ATTRIBUTE BYTES                   308
SVCTYP   DS    X                       SVC TYPE                          309
SVCTP1   EQU   B'00000000'             TYPE 1                            310
SVCTP2   EQU   B'10000000'             TYPE 2                            311
SVCTP34  EQU   B'11000000'             TYPE 3 OR 4                       312
SVCTP6   EQU   B'00100000'             TYPE 6                            313
SVCAPF   EQU   B'00001000'             REQUIRES APF AUTHORIZATION        314
SVCESR   EQU   B'00000100'             ESR                               315
SVCNP    EQU   B'00000010'             NON-PREEMPTIBLE                   316
SVCASF   EQU   B'00000001'             ASF                               317
SVCRESV1 DS    X                       RESERVED                          318
SVCLOCKS DS    XL2                     LOCKS                             319
SVCLL    EQU   B'10000000'             LOCAL LOCK                        320
SVCCMS   EQU   B'01000000'             CMS LOCK                          321
SVCOPT   EQU   B'00100000'                                               322
SVCALLOC EQU   B'00010000'             SALLOC  LOCK                      323
SVCDISP  EQU   B'00001000'             DISP LOCK                         324
SVCLENT  EQU   *-SVC                   LENGTH OF AN SVCTABLE ENTRY       325
*---------------------------------------------------------------------*  326
*        OTHER DSECTS                                                 *  327
*---------------------------------------------------------------------*  328
         PRINT NOGEN                                                     329
         CVT DSECT=YES,LIST=NO                                           330
         IHASCVT                                                         331
         END                                                             332
