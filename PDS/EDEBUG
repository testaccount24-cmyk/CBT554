*+--------------------------------------------------------------------+
*|                      MAINTENANCE LOG                               |
*+----------+-------------------------------------+---------+---------+
*|  DATE    |       DESCRIPTION                   | CHANGED | REFER   |
*+----------+-------------------------------------+---------+---------+
*| 04/12/92 |  SETRP CONTINUATION IN COLUMN 16    | MJM     | @01     |
*|          |    (FIX#-R0004)                     |         |         |
*+----------+-------------------------------------+---------+---------+
*                                                                     |
*+--------------------------------------------------------------------+    1
*|  FUNCTION:                                                         |    2
*|    SKELETON FOR INTERACTIVE DEBUGGER THAT RUNS AS AN ESTAE UNDER   |    3
*|    TSO.  (ACKNOWLEDGMENTS TO DAVID B. COLE)                        |    4
*|  LOGIC:                                                            |    5
*|    1) CHECK FOR SDWA.  IF NO SDWA, PERCOLATE THE ABEND.            |    6
*|    2) USE TPUT TO OUTPUT ENTRYPOINT ADDRESS, ABEND PSW, AND        |    7
*|       ABEND CODE.                                                  |    8
*|    3) CHECK WHETHER THE ABEND WAS CAUSED BY AN IMPLANTED           |    9
*|       @DB@ BREAKPOINT. IF YES, PROMPT FOR RETRY.                   |   10
*|    4) ISSUE SETRP RC=4 TO RETRY INTO THE ABENDING CODE OR          |   11
*|       SETRP RC=0 TO PERCOLATE THE ABEND                            |   12
*|  INPUTS:                                                           |   13
*|    R1 - ADDRESS OF SDWA SUPPLIED BY RTM2.                          |   14
*|  OUTPUTS:                                                          |   15
*|    MESSAGES ON TSO TERMINAL.                                       |   16
*|    MODIFICATIONS TO SDWA.                                          |   17
*|  ATTRIBUTES:                                                       |   18
*|    AMODE 31, RMODE 24, REENTERABLE                                 |   19
*+--------------------------------------------------------------------+   20
*+--------------------------------------------------------------------+
*|  Copyright (c) 1992, 1993, 2002 by Mitchell Marx & Penelope Davis  |
*|                                                                    |
*|  Address questions to: mmarx@mdpaladin.com                         |
*|                                                                    |
*|  Permission is hereby granted, free of charge, to any person       |
*|  obtaining a copy of this software and associated documentation    |
*|  files (the "Software"), to deal in the Software without           |
*|  restriction, including without limitation the rights to use,      |
*|  copy, modify, merge, publish, distribute, sublicense, and/or sell |
*|  copies of the Software, and to permit persons to whom the         |
*|  Software is furnished to do so, subject to the following          |
*|  conditions:                                                       |
*|                                                                    |
*|  The above copyright notice and this permission notice shall be    |
*|  included in all copies or substantial portions of the Software.   |
*|                                                                    |
*|  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,   |
*|  EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES   |
*|  OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND          |
*|  NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT       |
*|  HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,      |
*|  WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING      |
*|  FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR     |
*|  OTHER DEALINGS IN THE SOFTWARE.                                   |
*+--------------------------------------------------------------------+
EDEBUG   CSECT                                                            21
EDEBUG   AMODE 31                                                         22
EDEBUG   RMODE 24                                                         23
         USING EDEBUG,R15              SET ADDRESSING ON ENTRYPOINT       24
         B     BEGIN                   BRANCH AROUND DOCUMENTATION        25
         DC    C' EDEBUG '             CSECT NAME                         26
         DC    C' &SYSDATE '           DATE OF ASSEMBLY                   27
         DC    C' &SYSTIME '           TIME OF ASSEMBLY                   28
BEGIN    DS    0H                                                         29
         PRINT NOGEN                                                      30
         COPY  EQUATES                                                    31
         PRINT GEN                                                        32
*        *---------------------------------------*                        33
*        *     TEST WHETHER THERE IS AN SDWA     *                        34
*        *---------------------------------------*                        35
         CH    R0,=H'12'               WAS AN SDWA OBTAINED ?             36
         BNE   Y$SDWA                  YES; CONTINUE                      37
         LA    R15,0                   SET FOR PERCOLATION                38
         BR    R14                     RETURN TO RTM2                     39
Y$SDWA   DS    0H                                                         40
         DROP  R15                     DROP ADDRESSING ON ENTRYPOINT      41
         STM   R14,R12,12(R13)         SAVE REGS IN RTM2'S SAVEAREA       42
         LR    R12,R15                 LOAD ENTRYPOINT INTO BASE REG      43
         USING EDEBUG,R12              SET ADDRESSING ON BASE REGISTER    44
         GETMAIN RU,LV=LEDEBUG,        ..GET < 16M STORAGE FOR         X  45
               LOC=BELOW               ..REENTERABLE WORK AREA            46
         LR    R2,R13                  ADDRESS OF CALLER'S SAVE AREA      47
         ST    R13,4(R1)               SAVE CALLER'S SAVE AREA ADDRESS    48
         ST    R1,8(R13)               ADDRESS OF OUR SAVEAREA TO CALLR   49
         LR    R13,R1                  ADDRESS OF WORKAREA TO R13         50
         USING WEDEBUG,R13             MAP WORK AREA                      51
         LM    R14,R2,12(R2)           RESTORE REGS FROM CALLERS SAVEA    52
         LR    R3,R1                   ADDRESS OF SDWA                    53
         USING SDWA,R3                 MAP SDWA                           54
*        *---------------------------------------*                        55
*        *     UNCOMMENT THE FOLLOWING 2 LINES   *                        56
*        *     TO TEST FOR RECURSIVE ENTRY.      *                        57
*        *---------------------------------------*                        58
*        TM    SDWAERRD,SDWASTAE       RECURSIVE ENTRY ?                  59
*        BO    EXIT00                  YES; PERCOLATE THE ABEND           60
*        *---------------------------------------*                        61
*        *     OUTPUT ENTRY POINT ADDRESS        *                        62
*        *---------------------------------------*                        63
         MVC   PUTGET(L'MSG1),MSG1     MOVE MESSAGE TO OUTPUT AREA        64
         XTOC  PLIST=XTOCLIST,         ..CHANGE                        X  65
               ADIN=SDWAEPA,           ..HEX VALUE                     X  66
               LIN=L'SDWAEPA,          ..OF SDWA FIELD                 X  67
               ADOUT=PUTGET+L'MSG1,    ..TO PRINTABLE                  X  68
               LOUT=L'SDWAEPA*2        ..CHARACTERS                       69
         LA    R0,L'MSG1+L'SDWAEPA*2   LENGTH OF OUTPUT FIELD FOR TPUT    70
         LA    R1,PUTGET               ADDRESS OF OUTPUT AREA             71
         TPUT  (R1),(R0),R             SEND MESSAGE TO TERMINAL           72
*        *---------------------------------------*                        73
*        *     OUTPUT ABEND PSW                  *                        74
*        *---------------------------------------*                        75
         MVC   PUTGET(L'MSG2),MSG2     MOVE MESSAGE TO OUTPUT AREA        76
         XTOC  PLIST=XTOCLIST,         ..CHANGE                        X  77
               ADIN=SDWAEC1,           ..HEX VALUE                     X  78
               LIN=L'SDWAEC1,          ..OF SDWA FIELD                 X  79
               ADOUT=PUTGET+L'MSG2,    ..TO PRINTABLE                  X  80
               LOUT=L'SDWAEC1*2        ..CHARACTERS                       81
         LA    R0,L'MSG2+L'SDWAEC1*2   LENGTH OF OUTPUT FIELD FOR TPUT    82
         LA    R1,PUTGET               ADDRESS OF OUTPUT AREA             83
         TPUT  (R1),(R0),R             SEND MESSAGE TO TERMINAL           84
*        *---------------------------------------*                        85
*        *     OUTPUT ABEND CODE                 *                        86
*        *---------------------------------------*                        87
         MVC   PUTGET(L'MSG3),MSG3     MOVE MESSAGE TO OUTPUT AREA        88
         XTOC  PLIST=XTOCLIST,         ..CHANGE                        X  89
               ADIN=SDWAABCC,          ..HEX VALUE                     X  90
               LIN=L'SDWAABCC,         ..OF SDWA FIELD                 X  91
               ADOUT=PUTGET+L'MSG3,    ..TO PRINTABLE                  X  92
               LOUT=L'SDWAABCC*2       ..CHARACTERS                       93
         LA    R0,L'MSG3+L'SDWAABCC*2  LENGTH OF OUTPUT FIELD FOR TPUT    94
         LA    R1,PUTGET               ADDRESS OF OUTPUT AREA             95
         TPUT  (R1),(R0),R             SEND MESSAGE TO TERMINAL           96
*        *---------------------------------------*                        97
*        *     IF ABEND 0C1 CAUSED BY A @DB@     *                        98
*        *     TRAP, PROMPT FOR CONTINUATION.    *                        99
*        *     OTHERWISE PERCOLATE THE ABEND.    *                       100
*        *---------------------------------------*                       101
         XR    R1,R1                   CLEAR REGISTER                    102
         ICM   R1,B'0011',SDWACMPC     SYSTEM COMPLETION CODE            103
         SRL   R1,4                    RIGHT JUSTIFY                     104
         C     R1,S0C1                 IS IT 0C1 ?                       105
         BNE   EXIT00                  NO; PERCOLATE ABEND               106
         L     R1,SDWANXT1             A(NEXT INSTRUCTION) FROM PSW      107
         CLC   0(L'EDB,R1),EDB         IS IT OUR EDB TRAP ?              108
         BNE   EXIT00                  NO; PERCOLATE ABEND               109
         LA    R0,L'PROMPT             LENGTH OF PROMPT LITERAL          110
         LA    R1,PROMPT               ADDRESS OF PROMPT LITERAL         111
         TPUT  (R1),(R0),R             TPUT PROMPT MESSAGE               112
         LA    R0,L'PUTGET             LENGTH OF TPUT/TGET BUFFER        113
         LA    R1,PUTGET               ADDRESS OF TPUT/TGET BUFFER       114
         ICM   R1,B'1000',GETFLAGS     SET FLAG FOR TGET                 115
         TGET  (R1),(R0),R             GET TERMINAL RESPONSE INTO BUF    116
         OC    PUTGET(L'GO),FOLD       CHANGE RESPONSE TO UPPER CASE     117
         CLC   PUTGET(L'GO),GO         IS IT "GO" ?                      118
         BE    EXIT04                  YES; RETRY INTO CODE              119
         B     EXIT00                  PERCOLATE THE ABEND               120
*---------------------------------------------------------------------*  121
*        PERCOLATE THE ABEND                                          *  122
*---------------------------------------------------------------------*  123
EXIT00   DS    0H                                                        124
         LR    R2,R13                  A(SAVE AREA) => R2                125
         L     R13,SAVEGEN+4           RESTORE A(CALLER'S SAVE AREA)     126
         FREEMAIN RU,LV=LEDEBUG,A=(2)  FREE WORK AREA STORAGE            127
         LR    R1,R3                   SDWA ADDRESS TO R1                128
         SETRP RC=0,REGS=(14,12),DUMP=NO                                 129
*---------------------------------------------------------------------*  130
*        RETRY                                                        *  131
*---------------------------------------------------------------------*  132
EXIT04   DS    0H                                                        133
         LR    R2,R13                  A(SAVE AREA) => R2                134
         L     R13,SAVEGEN+4           RESTORE A(CALLER'S SAVE AREA)     135
         FREEMAIN RU,LV=LEDEBUG,A=(2)  FREE WORK AREA STORAGE            136
         L     R15,SDWANXT1            A(NEXT INSTRUCTION) FROM PSW      137
         LA    R15,L'EDB(R15)          BUMP FOR LENGTH OF EDB TRAP       138
         LR    R1,R3                   SDWA ADDRESS TO R1                139
         SETRP RC=4,RETREGS=YES,RETADDR=(15),FRESDWA=YES,              X 140
               REGS=(14,12),DUMP=NO                                 @01  141
*---------------------------------------------------------------------*  142
*        CONSTANTS                                                    *  143
*---------------------------------------------------------------------*  144
S0C1     DC    A(X'000000C1')          S0C1 COMPARE VALUE                145
EDB      DC    C'@DB@'                 EDB TRAP COMPARE VALUE            146
GETFLAGS DC    B'10000000'             FLAGS FOR TGET                    147
MSG1     DC    C'ENTRY POINT ADDRESS ... '                               148
MSG2     DC    C'PSW AT ABEND .......... '                               149
MSG3     DC    C'ABEND CODE ............ '                               150
PROMPT   DC    C'ENTER GO TO CONTINUE'                                   151
GO       DC    C'GO'                                                     152
FOLD     DC    (L'PUTGET)B'01000000'   UPPERCASE MASK                    153
         LTORG                                                           154
*---------------------------------------------------------------------*  155
*        MAP WORK AREA                                                *  156
*---------------------------------------------------------------------*  157
WEDEBUG  DSECT                         USER ACQUIRED REENTRABLE STORAGE  158
SAVEGEN  DS    18F                     GENERAL REGISTER SAVE AREA        159
XTOCLIST DS    4F                      PLIST FOR XTOC                    160
PUTGET   DS    CL256                   TPUT/TGET BUFFER                  161
LEDEBUG  EQU   *-WEDEBUG                                                 162
*---------------------------------------------------------------------*  163
*        OTHER DSECTS                                                 *  164
*---------------------------------------------------------------------*  165
         IHASDWA DSECT=YES        MAP SDWA                               166
         END                                                             167
