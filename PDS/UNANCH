*+--------------------------------------------------------------------+    1
*|  FUNCTION:                                                         |    2
*|      REMOVE A CONTROL BLOCK ANCHORED IN AN LPAQ CDE.               |    3
*|  LOGIC:                                                            |    4
*|      1)  SEARCH CDE'S ON LINK-PACK-AREA-QUEUE CHOSEN CDE.          |    5
*|      2)  IF NOT FOUND, EXIT.                                       |    6
*|      3)  VERIFY THAT ENTRY POINT MODULE CHOSEN.                    |    7
*|      4)  GET THE LOCAL AND CMS LOCKS TO SERIALIZE LPAQ.            |    8
*|      4)  SCAN THE LPAQ FOR THE CDE PREVIOUS IN THE CHAIN           |    9
*|      5)  UNCHAIN THE CDE FROM THE LPAQ.                            |   10
*|      6)  (FREEMAIN STORAGE FOR TABLE, CDE AND XL)                  |   11
*|      7)  RELEASE ALL LOCKS                                         |   12
*|  INPUTS:                                                           |   13
*|      R1 - ADDRESS OF PARMLIST MAPPED BY ANKPRM MACRO.              |   14
*|  OUTPUTS:                                                          |   15
*|      R15 - RETURN CODE                                             |   16
*|            0 = CDE HAS BEEN UNCHAINED OR DOES NOT EXIST            |   17
*|            8 = EYECATCHER VERIFICATION FAILED                      |   18
*|           12 = FREEMAIN FAILED FOR MODULE.                         |   19
*|           16 = FREEMAIN FAILED FOR CDE                             |   20
*|           20 = FREEMAIN FAILED FOR EXTENT LIST                     |   21
*|  ATTRIBUTES:                                                       |   22
*|      AMODE 31, RMODE ANY, REENTRANT                                |   23
*|      CALLER MUST BE IN PRIMARY SPACE MODE                          |   24
*+--------------------------------------------------------------------+   25
*+--------------------------------------------------------------------+
*|  Copyright (c) 1992, 1993, 2002 by Mitchell Marx & Penelope Davis  |
*|                                                                    |
*|  Address questions to: mmarx@mdpaladin.com                         |
*|                                                                    |
*|  Permission is hereby granted, free of charge, to any person       |
*|  obtaining a copy of this software and associated documentation    |
*|  files (the "Software"), to deal in the Software without           |
*|  restriction, including without limitation the rights to use,      |
*|  copy, modify, merge, publish, distribute, sublicense, and/or sell |
*|  copies of the Software, and to permit persons to whom the         |
*|  Software is furnished to do so, subject to the following          |
*|  conditions:                                                       |
*|                                                                    |
*|  The above copyright notice and this permission notice shall be    |
*|  included in all copies or substantial portions of the Software.   |
*|                                                                    |
*|  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,   |
*|  EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES   |
*|  OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND          |
*|  NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT       |
*|  HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,      |
*|  WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING      |
*|  FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR     |
*|  OTHER DEALINGS IN THE SOFTWARE.                                   |
*+--------------------------------------------------------------------+
         MODULE UNANCH,BASE=12,LOC=BELOW,AMODE=31,RMODE=ANY,           X  26
               TEXT='UNCHAIN CONTROL BLOCK ON LPAQ'                       27
*        *---------------------------------------*                        28
*        *     MAP CONTROL BLOCKS USED           *                        29
*        *---------------------------------------*                        30
         USING CVT,R3                  MAP CVT                            31
         USING CDENTRY,R4              MAP CDE                            32
         USING XTLST,R6                MAP EXTENT LIST                    33
         USING ANKPRM,R10              MAP PARMLIST                       34
         LR    R10,R1                  A(PARMLIST) => R10                 35
*---------------------------------------------------------------------*   36
*        SCAN LINK PACK AREA QUEUE FOR CDE CONTAINING MODULE ANCHOR   *   37
*---------------------------------------------------------------------*   38
         L     R3,CVTPTR               A(CVT) => R3                       39
         LA    R9,ANKNAME              A(MODULE NAME) => R9               40
         L     R8,CVTQLPAQ             HEAD OF LPAQ => R8                 41
         L     R15,CVTQCDSR            A(LPAQ SCAN ROUTINE)               42
         BALR  R14,R15                 INVOKE LPAQ SCAN ROUTINE           43
         B     FNDCDE                  CDE FOUND; BRANCH TO PROCESS CDE   44
         B     EXIT00                  CDE NOT FOUND; EXIT                45
*---------------------------------------------------------------------*   46
*        IF CDE FOUND, CHECK STORAGE IT POINTS TO FOR EYECATCHER      *   47
*---------------------------------------------------------------------*   48
FNDCDE   DS    0H                                                         49
         LR    R4,R11                  A( CDE ) FROM CVTQCDSR ROUTINE     50
         ST    R11,ADCDE               SAVE ADDRESS OF CDE                51
         TM    ANKFLAG1,@ANKVFY        VERIFY THE STORAGE ?               52
         BNO   NOVFY                   NO; SKIP VERIFICATION              53
         L     R5,CDENTPT              ENTRY POINT FROM CDE => R5         54
         CLC   0(L'ANKEYE,R5),ANKEYE   IS EYCATCHER FIRST BYTES IN STG?   55
         BNE   EXIT08                  NO; EXIT                           56
NOVFY    DS    0H                                                         57
         MODESET MODE=SUP,KEY=ZERO     GET SUPERVISOR/KEY 0 PSW           58
*        *---------------------------------------*                        59
*        *      GET THE LOCAL LOCK               *                        60
*        *---------------------------------------*                        61
         SETLOCK OBTAIN,                                               X  62
               MODE=UNCOND,                                            X  63
               TYPE=LOCAL,                                             X  64
               REGS=USE,                                               X  65
               RELATED=REQUIRED                                           66
*        *---------------------------------------*                        67
*        *      GET CMS LOCK                     *                        68
*        *---------------------------------------*                        69
         SETLOCK OBTAIN,                                               X  70
               MODE=UNCOND,                                            X  71
               TYPE=CMS,                                               X  72
               REGS=USE,                                               X  73
               RELATED=REQUIRED                                           74
*---------------------------------------------------------------------*   75
*        FIND PREVIOUS CDE IN LPAQ                                    *   76
*---------------------------------------------------------------------*   77
         L     R4,CVTQLPAQ             HEAD OF LPAQ => R8                 78
         LA    R5,CDCHAIN-CDENTRY      .. BACK UP FOR DISPLACEMENT        79
         SLR   R4,R5                   .. IN CDE TO CHAIN FIELD           80
SCDLP    DS    0H                                                         81
         CLC   CDCHAIN(L'CDCHAIN),ADCDE DOES THIS POINT TO OUR CDE?       82
         BE    FNDPREV                 YES; THIS IS THE PREVIOUS CDE      83
         ICM   R4,15,CDCHAIN           POINT AT NEXT CDE                  84
         BNZ   SCDLP                   CHECK CHAIN FIELD IN NEXT CDE      85
         ABEND 123                                                        86
*---------------------------------------------------------------------*   87
*        UNCHAIN CDE FROM LPAQ                                        *   88
*---------------------------------------------------------------------*   89
FNDPREV  DS    0H                                                         90
         L     R5,ADCDE                ADDRESS OF TARGET CDE              91
         L     R5,CDCHAIN-CDENTRY(R5) CHAIN POINTER FROM TARGET CDE       92
         ST    R5,CDCHAIN              SAVE IN PREVIOUS ENTRY CHAIN FLD   93
*        *---------------------------------------*                        94
*        *      RELEASE ALL LOCKS                *                        95
*        *---------------------------------------*                        96
         SETLOCK RELEASE,                                              X  97
               TYPE=ALL,                                               X  98
               REGS=USE,                                               X  99
               RELATED=REQUIRED                                          100
*---------------------------------------------------------------------*  101
*        VERIFY THAT THE OLD MODULE IS ALLOCATED IN THE RIGHT SUBPOOL *  102
*        IF SO, FREEMAIN OLD MODULE, CDE AND EXTENT LIST ENTRY        *  103
*---------------------------------------------------------------------*  104
         TM    ANKFLAG1,@ANKFREE       FREE OLD MODULE STORAGE  ?        105
         BNO   EXIT00                  NO; EXIT                          106
         L     R4,ADCDE                ADDRESS OF TARGET CDE             107
         L     R6,CDXLMJP              A(EXTENT LIST)                    108
         L     R8,CDENTPT              A(ENTRY POINT)                    109
         XR    R7,R7                                                     110
         ICM   R7,B'0111',XTLMSBLN     LENGTH OF THE MODULE              111
         VSMLOC SQA,AREA=((R8),(R7)) TEST SQA ALLOCATION                 112
         LTR   R15,R15                 ALLOCATED ?                       113
         BZ    CKSP                    YES; CHECK SUBPOOL                114
         VSMLOC CSA,AREA=((R8),(R7)) TEST CSA ALLOCATION                 115
         LTR   R15,R15                 ALLOCATED ?                       116
         BNZ   NOFREE                  NO; DO NOT FREEMAIN               117
CKSP     DS    0H                                                        118
         STCM  0,B'0001',TESTSP        SAVE SUBPOOL NUMBER IN TEST       119
         CLC   TESTSP(L'ANKSP),ANKSP   IS THE STORAGE IN THIS SUBPOOL?   120
         BNE   NOFREE                  NO; DO NOT FREEMAIN               121
         XR    R5,R5                   .. SUBPOOL NUMBER                 122
         ICM   R5,B'0001',TESTSP       .. FOR FREEMAIN                   123
         FREEMAIN RC,LV=(7),A=(8),SP=(5)                                 124
         LTR   R15,R15                 FREEMAIN SUCCESSFUL ?             125
         BNZ   EXIT0C                  NO; EXIT WITH ERROR               126
         LA    R0,LCDE                 LENGTH OF A CDE                   127
         FREEMAIN RC,LV=(0),A=(4),SP=245  FREE THE CDE                   128
         LTR   R15,R15                                                   129
         BNZ   EXIT10                                                    130
         LA    R0,LXTNT                LENGTH OF AN EXTENT LIST          131
         FREEMAIN RC,LV=(0),A=(6),SP=245  FREE THE XTENT LIST            132
         LTR   R15,R15                                                   133
         BNZ   EXIT14                                                    134
NOFREE   DS    0H                                                        135
         MODESET MODE=PROB,KEY=NZERO                                     136
         B     EXIT00                                                    137
*---------------------------------------------------------------------*  138
*        EXIT ROUTINES                                                *  139
*---------------------------------------------------------------------*  140
EXIT00   DS    0H                      SUCCESSFUL                        141
         LA    15,X'00'                                                  142
         B     EXIT                                                      143
EXIT08   DS    0H                      STORAGE VERIFICATION FAILED       144
         LA    15,X'08'                                                  145
         B     EXIT                                                      146
EXIT0C   DS    0H                      FREEMAIN FAILED FOR MODULE        147
         LA    15,X'0C'                                                  148
         B     EXIT                                                      149
EXIT10   DS    0H                      FREEMAIN FAILED FOR CDE           150
         LA    15,X'10'                                                  151
         B     EXIT                                                      152
EXIT14   DS    0H                      FREEMAIN FAILED FOR XL            153
         LA    15,X'14'                                                  154
         B     EXIT                                                      155
         EJECT                                                           156
*---------------------------------------------------------------------*  157
*        COMMON EXIT                                                  *  158
*---------------------------------------------------------------------*  159
EXIT     DS   0H                                                         160
         MODESET MODE=PROB,KEY=NZERO                                     161
         ENDMOD                                                          162
*---------------------------------------------------------------------*  163
*        WORK AREA                                                    *  164
*---------------------------------------------------------------------*  165
WUNANCH DSECT                                                            166
ADCDE    DS    A                  A( CDE )                               167
TESTSP   DS    X                  TEST SUBPOOL BYTE                      168
LUNANCH EQU    *-WUNANCH                                                 169
*---------------------------------------------------------------------*  170
*        OTHER DSECTS                                                 *  171
*---------------------------------------------------------------------*  172
         ANKPRM                    MAP PARMLIST                          173
         IHACDE                    MAP CDE                               174
LCDE     EQU   *-CDENTRY           LENGTH OF CDE                         175
         IHAXTLST                  MAP EXTENT LIST                       176
LXTNT    EQU   *-XTLST             LENGTH OF EXTENT LIST                 177
         CVT    DSECT=YES,LIST=YES MAP CVT                               178
         IHAPSA DSECT=YES,LIST=YES MAP PSA FOR SETLOCK                   179
         END                                                             180
