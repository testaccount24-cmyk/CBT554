*+--------------------------------------------------------------------+    1
*| FUNCTION:                                                          |    2
*|   ESTABLISH ACCESS REGISTER COMMUNICATION WITH ANY FULL FUNCTION   |    3
*|   (I.E. NOT DATASPACE OR HIPERSPACE) ADDRESS SPACE                 |    4
*| INPUTS:                                                            |    5
*|    R1 - ADDRESS OF PARMLIST SUPPLIED BY CALLER MAPPED BY XMSESA    |    6
*| OUTPUTS:                                                           |    7
*|    ACCESS LIST ENTRY FOR TARGET ADDRESS SPACE ON DU-AL             |    8
*|    (DISPATCHABLE UNIT-ACCESS LIST).                                |    9
*|    ALET FOR ACCESS LIST ENTRY IN XMSESA PARMLIST.                  |   10
*| R15 RETURN CODE:                                                   |   11
*|     0 - ACCESS LIST ENTRY CREATED FOR ADDRESS SPACE                |   12
*|     8 - OPERATING SYSTEM IS NOT SP3 OR ABOVE (ESA).                |   13
*|    12 - GETASCB SCAN ROUTINE IS NOT LINKEDITED                     |   14
*|    16 - GETASCB SCAN ROUTINE DID NOT FIND TARGET ADDRESS SPACE     |   15
*|    20 - ALESERV FAILED. RETURN CODE IN R0, REASON CODE IN R1.      |   16
*| LOGIC:                                                             |   17
*|    1) CHECK OPERATING SYSTEM LEVEL FOR MVS/ESA                     |   18
*|    2) CALL GETASCB SCAN ROUTINE TO GET A(ASCB) OF TARGET ADDRESS   |   19
*|       SPACE                                                        |   20
*|    3) GET STOKEN OF TARGET ADDRESS SPACE FROM ASSB. (ASCB=>ASSB)   |   21
*|    4) USE STOKEN AS INPUT TO ALESERV TO MAKE ACCESS LIST ENTRY.    |   22
*| ATTRIBUTES:                                                        |   23
*|    AMODE=31, RMODE=ANY, REENTRANT, APF AUTHORIZED                  |   24
*+--------------------------------------------------------------------+   25
*+--------------------------------------------------------------------+
*|  Copyright (c) 1992, 1993, 2002 by Mitchell Marx & Penelope Davis  |
*|                                                                    |
*|  Address questions to: mmarx@mdpaladin.com                         |
*|                                                                    |
*|  Permission is hereby granted, free of charge, to any person       |
*|  obtaining a copy of this software and associated documentation    |
*|  files (the "Software"), to deal in the Software without           |
*|  restriction, including without limitation the rights to use,      |
*|  copy, modify, merge, publish, distribute, sublicense, and/or sell |
*|  copies of the Software, and to permit persons to whom the         |
*|  Software is furnished to do so, subject to the following          |
*|  conditions:                                                       |
*|                                                                    |
*|  The above copyright notice and this permission notice shall be    |
*|  included in all copies or substantial portions of the Software.   |
*|                                                                    |
*|  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,   |
*|  EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES   |
*|  OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND          |
*|  NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT       |
*|  HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,      |
*|  WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING      |
*|  FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR     |
*|  OTHER DEALINGS IN THE SOFTWARE.                                   |
*+--------------------------------------------------------------------+
         MODULE XTALK,BASE=12,LOC=BELOW,AMODE=31,RMODE=ANY,            X  26
               TEXT='BUILD ACCESS LIST ENTRY ANY ADDRESS SPACE'           27
         LR    R3,R1                   PARMLIST                           28
         USING XMSESA,R3               MAP PARMLIST                       29
*        *---------------------------------------*                        30
*        *   CHECK IF MVS SP3 OR ABOVE (MVSESA)  *                        31
*        *---------------------------------------*                        32
         L     R4,CVTPTR               A(CVT)                             33
         S     R4,=A(CVTMAP-CVTFIX)    BACKUP FOR LENGTH OF CVT PREFIX    34
         USING CVTFIX,R4               MAP CVT BEGINNING AT PREFIX        35
         LA    R4,CVTPRODN-CVTFIX(R4)  POINT TO MVS VERSION               36
         CLC   0(L'SP3,R4),SP3         IS THIS SP3 OR ABOVE               37
         BL    EXIT08                  NO; EXIT WITH RC=8 ERROR           38
*        *---------------------------------------*                        39
*        *   CALL SCAN OF ASVT TO OBTAIN A(ASCB) *                        40
*        *---------------------------------------*                        41
         LR    R1,R3                   PARMLIST                           42
         ICM   R15,15,=V(GETASCB)      A(ROUTINE TO R R15)                43
         BZ    EXIT0C                  GETASCB NOT LINKEDITED             44
         O     R15,=A(X'80000000')     SET FOR 31 BIT                     45
         BASSM R14,R15                 INVOKE ROUTINE                     46
         LTR   R15,R15                 WAS ASCB FOUND                     47
         BNZ   EXIT10                  NO; EXIT WITH RC=16                48
*        *---------------------------------------*                        49
*        *   GET STOKEN OF TARGET ADDRESS SPACE  *                        50
*        *---------------------------------------*                        51
         DROP  R4                      DROP R4 ADDRESSABILITY             52
         L     R4,XMASCB               A(ASCB) OF TARGET ADDRESS SPACE    53
         USING ASCB,R4                 MAP ASCB                           54
         L     R5,ASCBASSB             A(ASSB) OF TARGET ADDRESS SPACE    55
         USING ASSB,R5                 MAP ASSB                           56
*                                      MOVE STOKEN OF TARGET TO PARMS     57
         MVC   XMSTOKEN(L'XMSTOKEN),ASSBSTKN                              58
         SPKA  0                       GET A KEY ZERO PSW                 59
*        *---------------------------------------*                        60
*        *     CREATE ENTRY IN ACCESS LIST       *                        61
*        *---------------------------------------*                        62
         ALESERV ADD,                                                  X  63
               STOKEN=XMSTOKEN,        STOKEN FROM ASSB OF TARGET      X  64
               ALET=XMALET,            ALET RETURNED BY ALESERV        X  65
               CHKEAX=NO,              DO NOT CHECK EAX                X  66
               AL=WORKUNIT,            USE THIS TCB'S ACCESS LIST      X  67
               ACCESS=PUBLIC,          CREATE PUBLIC ACCESS LIST ENTRY X  68
               MF=(E,ALELIST)                                             69
         LTR   R15,R15                 RETURN CODE = 0                    70
         BNZ   EXIT14                  NO, EXIT RC=20.                    71
         B     EXIT00                                                     72
*---------------------------------------------------------------------*   73
*        EXIT ROUTINES                                                *   74
*---------------------------------------------------------------------*   75
EXIT00   DS    0H                      SUCCESSFUL                         76
         LA    R15,X'00'                                                  77
         B     EXIT                                                       78
EXIT08   DS    0H                      NOT EXECUTING ON ESA SYSTEM        79
         LA    R15,X'08'                                                  80
         B     EXIT                                                       81
EXIT0C   DS    0H                      ASVT SCAN ROUTINE NOT LINKED       82
         LA    R15,X'0C'                                                  83
         B     EXIT                                                       84
EXIT10   DS    0H                      TARGET ASCB NOT FOUND BY SCAN      85
         LA    R15,X'10'                                                  86
         B     EXIT                                                       87
EXIT14   DS    0H                      ALESERV FAILED                     88
         PERCRC RC=X'14'                                                  89
         B     EXIT                                                       90
*---------------------------------------------------------------------*   91
*        COMMON EXIT                                                  *   92
*---------------------------------------------------------------------*   93
EXIT     DS    0H                                                         94
         ENDMOD                                                           95
*---------------------------------------------------------------------*   96
*        CONSTANTS                                                    *   97
*---------------------------------------------------------------------*   98
SP3      DC    C'SP3'    MVS/ESA                                          99
*---------------------------------------------------------------------*  100
*        WORK AREA                                                    *  101
*---------------------------------------------------------------------*  102
WXTALK   DSECT                                                           103
ALELIST  ALESERV MF=L                  PARMLIST FOR ALESERV              104
LXTALK   EQU  *-WXTALK                                                   105
*---------------------------------------------------------------------*  106
*        OTHER DSECTS                                                 *  107
*---------------------------------------------------------------------*  108
         XMSESA                        MAP PARMLIST FOR GETASCB          109
         CVT     DSECT=YES,PREFIX=YES,LIST=YES     MAP CVT               110
         IHAASCB DSECT=YES,LIST=YES                MAP ASCB              111
         IHAASSB LIST=YES                          MAP ASSB              112
         END                                                             113
