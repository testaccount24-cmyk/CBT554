*+--------------------------------------------------------------------+    1
*|  FUNCTION:                                                         |    2
*|    INVOKE THE HOOKIN PROGRAM TO INSTALL AN SVC FRONT-END.          |    3
*|  INPUTS:                                                           |    4
*|    NONE                                                            |    5
*|  OUTPUTS:                                                          |    6
*|    SVC FRONT-END INSTALLED                                         |    7
*|  LOGIC:                                                            |    8
*|    1) COPY SVC UPDATE MF=L TO USER PARMLIST                        |    9
*|    2) PUT NAME OF FRONT-END PROGRAM IN PARMLIST                    |   10
*|    3) INVOKE THE HOOKIN PROGRAM TO INSTALL THE FRONT-END           |   11
*|  FUNCTION:                                                         |   12
*|    AMODE 31, RMODE ANY, REENTRANT, REQUIRES APF AUTHORIZATION      |   13
*+--------------------------------------------------------------------+   14
*+--------------------------------------------------------------------+
*|  Copyright (c) 1992, 1993, 2002 by Mitchell Marx & Penelope Davis  |
*|                                                                    |
*|  Address questions to: mmarx@mdpaladin.com                         |
*|                                                                    |
*|  Permission is hereby granted, free of charge, to any person       |
*|  obtaining a copy of this software and associated documentation    |
*|  files (the "Software"), to deal in the Software without           |
*|  restriction, including without limitation the rights to use,      |
*|  copy, modify, merge, publish, distribute, sublicense, and/or sell |
*|  copies of the Software, and to permit persons to whom the         |
*|  Software is furnished to do so, subject to the following          |
*|  conditions:                                                       |
*|                                                                    |
*|  The above copyright notice and this permission notice shall be    |
*|  included in all copies or substantial portions of the Software.   |
*|                                                                    |
*|  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,   |
*|  EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES   |
*|  OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND          |
*|  NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT       |
*|  HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,      |
*|  WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING      |
*|  FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR     |
*|  OTHER DEALINGS IN THE SOFTWARE.                                   |
*+--------------------------------------------------------------------+
         MODULE HOOKDRV,BASE=12,AMODE=31,RMODE=ANY,                    X  15
               TEXT='CALL HOOKIN TO INSTALL SVC FRONT-END'                16
         LA    R3,PARMLIST                 ADDRESS OF PARMLIST => R3      17
         USING HOOKPRM,R3                  MAP PARMLIST                   18
*                                          ..COPY SVCUPDTE PARMLIST       19
         MVC   HOSVCUL(LHOSVCUL),SVCINSTL  ..MODEL TO USER PARMLIST       20
         LA    R1,L'HOOKNAME               LENGTH OF MODULE NAME          21
         STH   R1,HOLNAME                  PUT IN PARMLIST                22
         MVC   HONAME(L'HOOKNAME),HOOKNAME MODULE NAME TO PARMLIST        23
         LOAD  EP=HOOKIN,ERRET=EXIT08      LOAD THE INSTALL ROUTINE       24
         LR    R15,R0                      HOOKIN ENTRYPOINT TO R15       25
         LA    R1,PARMLIST                 PARMLIST ADDRESS TO R1         26
         BASSM R14,R15                     INVOKE THE INSTALL ROUTINE     27
         LTR   R15,R15                     RETURN CODE = 0 ?              28
         BNZ   EXIT0C                      NO; EXIT                       29
         B     EXIT00                                                     30
*---------------------------------------------------------------------*   31
*        EXIT ROUTINES                                                *   32
*---------------------------------------------------------------------*   33
EXIT00   DS    0H                          SUCCESSFUL INSTALL             34
         LA    15,X'00'                                                   35
         B     EXIT                                                       36
EXIT08   DS    0H                          LOAD FOR HOOKIN FAILED         37
         LA    15,X'08'                                                   38
         B     EXIT                                                       39
EXIT0C   DS    0H                          HOOKIN SERVICE FAILED          40
         PERCRC RC=X'0C'                                                  41
         B     EXIT                                                       42
*---------------------------------------------------------------------*   43
*        COMMON EXIT                                                  *   44
*---------------------------------------------------------------------*   45
EXIT     DS   0H                                                          46
         ENDMOD                                                           47
*---------------------------------------------------------------------*   48
*        NAME OF THE SVC FRONT-END MODULE AND                         *   49
*        SVCUPDTE PASSED TO HOOKIN PROGRAM IN PARMLIST                *   50
*---------------------------------------------------------------------*   51
HOOKNAME DC    CL8'HOOKSVC'            NAME OF THE SVC HOOK MODULE        52
SVCINSTL SVCUPDTE 200,REPLACE,TYPE=4,MF=L                                 53
*---------------------------------------------------------------------*   54
*        WORK AREA                                                    *   55
*---------------------------------------------------------------------*   56
WHOOKDRV DSECT                                                            57
PARMLIST DS    0F,CL(LHOOKPRM)         PARMLIST FOR HOOKIN SERVICE        58
LHOOKDRV EQU   *-WHOOKDRV              LENGTH OF WORK AREA                59
*---------------------------------------------------------------------*   60
*        OTHER DSECTS                                                 *   61
*---------------------------------------------------------------------*   62
         HOOKPRM                       MAP PARMLIST                       63
         END                                                              64
