*+--------------------------------------------------------------------+    1
*|  FUNCTION:                                                         |    2
*|    INSTALL AN SVC FRONT-END (HOOK). THE HOOK MODULE MUST BEGIN     |    3
*|    WITH THE PREFIX MAPPED BY THE HOOKPRE DSECT.                    |    4
*|  LOGIC:                                                            |    5
*|    1) CALL REPMOD TO INSTALL HOOK MODULE ON LPAQ.                  |    6
*|    2) PUT LENGTH OF HOOK MODULE IN HOOK PREFIX.                    |    7
*|    3) ENQUE ON SVC TABLE.                                          |    8
*|    4) LOCATE SVC TABLE FOR SVC.                                    |    9
*|       PUT ORIGINAL SVC ENTRYPOINT INTO HOOK MODULE PREFIX.         |   10
*|    5) ISSUE SVCUPDTE AGAINST SVCUPDTE MF=L IN INPUT PARMLIST       |   11
*|    6) DEQUE ON SVC TABLE.                                          |   12
*|  INPUT:                                                            |   13
*|    R1 -  ADDRESS OF PARMLIST MAPPED BY HOOKPRM DSECT               |   14
*|  OUTPUT:                                                           |   15
*|    R15 - RETURN CODE                                               |   16
*|           0 = HOOK SUCCESSFULLY INSTALLED                          |   17
*|           4 = REPMOD NOT LINK-EDITED                               |   18
*|           8 = REPMOD SERVICE FAILED                                |   19
*|          12 = UPDATE OF SVC TABLE FAILED                           |   20
*|  ATTRIBUTES:                                                       |   21
*|    AMODE 31 RMODE ANY REENTRANT                                    |   22
*+--------------------------------------------------------------------+   23
*+--------------------------------------------------------------------+
*|  Copyright (c) 1992, 1993, 2002 by Mitchell Marx & Penelope Davis  |
*|                                                                    |
*|  Address questions to: mmarx@mdpaladin.com                         |
*|                                                                    |
*|  Permission is hereby granted, free of charge, to any person       |
*|  obtaining a copy of this software and associated documentation    |
*|  files (the "Software"), to deal in the Software without           |
*|  restriction, including without limitation the rights to use,      |
*|  copy, modify, merge, publish, distribute, sublicense, and/or sell |
*|  copies of the Software, and to permit persons to whom the         |
*|  Software is furnished to do so, subject to the following          |
*|  conditions:                                                       |
*|                                                                    |
*|  The above copyright notice and this permission notice shall be    |
*|  included in all copies or substantial portions of the Software.   |
*|                                                                    |
*|  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,   |
*|  EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES   |
*|  OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND          |
*|  NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT       |
*|  HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,      |
*|  WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING      |
*|  FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR     |
*|  OTHER DEALINGS IN THE SOFTWARE.                                   |
*+--------------------------------------------------------------------+
         MODULE HOOKIN,BASE=12,AMODE=31,RMODE=ANY,                     X  24
               TEXT='INSTALL SVC HOOK ROUTINE'                            25
         LR    R3,R1                   ADDRESS OF INPUT PARMLIST          26
         USING HOOKPRM,R3              MAP INPUT PARMLIST                 27
*        *---------------------------------------*                        28
*        *     CALL REPMOD TO INSTALL HOOK       *                        29
*        *     MODULE ON LINK-PACK-AREA-QUEUE.   *                        30
*        *     REPMOD IS USUALLY CALLED:         *                        31
*        *      // EXEC PGM=REPMOD,PARM=HOOKMOD  *                        32
*        *---------------------------------------*                        33
         LA    R1,HOLNAME              ..STORE ADDRESS OF FIELD WITH      34
         ST    R1,ADHOLNAM             ..LNGTH AND NAME OF HOOK MODULE    35
         LA    R1,ADHOLNAM             LOAD ADDRESS OF ADDRESS INTO R1    36
         ICM   R15,15,=V(REPMOD)       ADDRESS OF REPMOD ROUTINE          37
         BZ    EXIT04                  =0; NOT LINKED, EXIT               38
         O     R15,=A(X'80000000')     SET FOR 31-BIT ENTRY               39
         BASSM R14,R15                 INVOKE REPMOD SERVICE              40
         LTR   R15,R15                 RETURN CODE= 0 ?                   41
         BNZ   EXIT08                  NO; EXIT                           42
*        *---------------------------------------*                        43
*        *     UPDATE HOOK MODULE PREFIX         *                        44
*        *---------------------------------------*                        45
         LR    R5,R0                   ADDRESS OF HOOK MODULE             46
         USING HOOKPRE,R5              MAP PREFIX IN HOOK MODULE          47
         ST    R1,HOPLSTG              STORE HOOK MOD LENGTH IN PREFIX    48
         LA    R2,LHOOKPRE             ..LENGTH OF PREFIX                 49
         ST    R2,HOPLPRE              ..INTO PREFIX                      50
*        *---------------------------------------*                        51
*        *     ENQUE ON SVCTABLE                 *                        52
*        *---------------------------------------*                        53
         MODESET MODE=SUP,KEY=ZERO     SUPERVISOR STATE/KEY 0 PSW         54
         ENQ   (QNAME,RNAME,E,L'RNAME,SYSTEM)  ENQUE ON SVC TABLE         55
*        *---------------------------------------*                        56
*        *     LOCATE SVCTABLE ENTRY FOR SVC.    *                        57
*        *     PUT SVC ENTRYPOINT IN HOOK PREFIX.*                        58
*        *---------------------------------------*                        59
         L     R2,CVTPTR               ADDRESS OF THE CVT                 60
         L     R2,CVTABEND-CVT(R2)     ADDRESS OF THE SCVT                61
         L     R2,SCVTSVCT-SCVTSECT(R2)  ADDRESS OF THE SVC TABLE         62
         XR    R6,R6                   CLEAR R6                           63
         ICM   R6,1,HOSVC              SVC NUMBER                         64
         SLL   R6,3                    * 8                                65
         AR    R2,R6                   = ADDRESS OF ENTRY                 66
         L     R6,0(R2)                LOAD SVC ROUTINE ENTRY POINT       67
         ST    R6,HOPORIG              STORE ENTRYPOINT IN HOOK PREFIX    68
*        *---------------------------------------*                        69
*        *     POINT SVCTABLE ENTRY TO HOOK MOD  *                        70
*        *---------------------------------------*                        71
*                                      ..ADDRESS OF HOOK MODULE +         72
         LA    R7,LHOOKPRE(R5)         ..LENGTH OF PREFIX = ENTRYPT       73
         LA    R2,HOSVCUL                ..ISSUE SVCUPDTE USING SVCUPDT   74
         SVCUPDTE EP=(R7),MF=(E,(2))     ..PARMLIST FROM CALLER           75
         LR    R6,R15                  PRESERVE RETURN CODE               76
*        *---------------------------------------*                        77
*        *     DEQUE ON SVCTABLE                 *                        78
*        *---------------------------------------*                        79
         DEQ   (QNAME,RNAME,L'RNAME,SYSTEM)   DEQUE ON SVCTABLE           80
         MODESET MODE=PROB,KEY=NZERO   SET PROBLEM STATE/KEY 8 PSW        81
         LTR   R6,R6                   RETURN CODE FROM SVCUPDTE = 0 ?    82
         BNZ   EXIT0C                  NO; EXIT                           83
         B     EXIT00                                                     84
*---------------------------------------------------------------------*   85
*        EXIT ROUTINES                                                *   86
*---------------------------------------------------------------------*   87
EXIT00   DS    0H                      SUCCESSFUL INSTALL                 88
         LA    15,X'00'                                                   89
         B     EXIT                                                       90
EXIT04   DS    0H                      REPMOD NOT LINK-EDITED             91
         LA    15,X'04'                                                   92
         B     EXIT                                                       93
EXIT08   DS    0H                      REPMOD SERVICE FAILED              94
         LA    15,X'08'                                                   95
         B     EXIT                                                       96
EXIT0C   DS    0H                      SVCUPDTE FAILED                    97
         PERCRC RC=X'0C'                                                  98
         B     EXIT                                                       99
*---------------------------------------------------------------------*  100
*        COMMON EXIT                                                  *  101
*---------------------------------------------------------------------*  102
EXIT     DS   0H                                                         103
         ENDMOD                                                          104
*---------------------------------------------------------------------*  105
*        CONSTANTS                                                    *  106
*---------------------------------------------------------------------*  107
QNAME    DC    CL8'SYSZSVC'            ENQ/DEQ MAJOR NAME                108
RNAME    DC    C'TABLE'                ENQ/DEQ MINOR NAME                109
*---------------------------------------------------------------------*  110
*        WORK AREA                                                    *  111
*---------------------------------------------------------------------*  112
WHOOKIN  DSECT                         AQUIRED STORAGE FOR REENTRABLE    113
ADHOLNAM DS    F                       ADDRESS ADHOLNAM                  114
LHOOKIN  EQU   *-WHOOKIN               LENGTH OF WORK AREA               115
*---------------------------------------------------------------------*  116
*        OTHER DSECTS                                                 *  117
*---------------------------------------------------------------------*  118
         HOOKPRM                       MAP INPUT PARMLIST                119
         HOOKPRE                       MAP HOOK PREFIX                   120
         CVT DSECT=YES,LIST=NO         MAP THE CVT                       121
         IHASCVT                       MAP THE SCVT                      122
         END                                                             123
