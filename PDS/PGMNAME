*+--------------------------------------------------------------------+    1
*|  FUNCTION:                                                         |    2
*|    GET ADDRESS SPACE NAME AND LENGTH                               |    3
*|  INPUT:                                                            |    4
*|    R1 = A(PARAMETER LIST) MAPPED BY PPGNM DSECT                    |    5
*|  OUTPUT:                                                           |    6
*|    R15 - RETURN CODE                                               |    7
*|  LOGIC:                                                            |    8
*|    1) GET CURRENT ASCB ADDRESS FROM PSA                            |    9
*|    2) SEE IF ASCBJBNI OR ASCBJBNS POINTS TO ADDRESS SPACE NAME     |   10
*|    3) MOVE ALL 8 BYTES OF NAME TO INPUT PARMLIST                   |   11
*|    4) FIND ADDRESS OF FIRST X'40' IN ADDRESS SPACE NAME AND        |   12
*|       CALCULATE LENGTH OF ADDRESS SPACE NAME                       |   13
*|  ATTRIBUTES:                                                       |   14
*|    AMODE 31, RMODE ANY, REENTRANT                                  |   15
*+--------------------------------------------------------------------+   16
*+--------------------------------------------------------------------+
*|  Copyright (c) 1992, 1993, 2002 by Mitchell Marx & Penelope Davis  |
*|                                                                    |
*|  Address questions to: mmarx@mdpaladin.com                         |
*|                                                                    |
*|  Permission is hereby granted, free of charge, to any person       |
*|  obtaining a copy of this software and associated documentation    |
*|  files (the "Software"), to deal in the Software without           |
*|  restriction, including without limitation the rights to use,      |
*|  copy, modify, merge, publish, distribute, sublicense, and/or sell |
*|  copies of the Software, and to permit persons to whom the         |
*|  Software is furnished to do so, subject to the following          |
*|  conditions:                                                       |
*|                                                                    |
*|  The above copyright notice and this permission notice shall be    |
*|  included in all copies or substantial portions of the Software.   |
*|                                                                    |
*|  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,   |
*|  EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES   |
*|  OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND          |
*|  NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT       |
*|  HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,      |
*|  WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING      |
*|  FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR     |
*|  OTHER DEALINGS IN THE SOFTWARE.                                   |
*+--------------------------------------------------------------------+
         MODULE PGMNAME,BASE=12,AMODE=31,RMODE=ANY,                    X  17
               TEXT='GET ADDRESS SPACE NAME'                              18
*        *---------------------------------------*                        19
*        *     DETERMINE LENGTH OF NAME          *                        20
*        *---------------------------------------*                        21
         LR    R3,R1                   ADDRESS OF INPUT PARMLIST          22
         USING PPGNM,R3                MAP PARMLIST                       23
         L     R4,PSAAOLD-PSA          A(CURRENT ASCB) FROM PSA           24
*                                      INITIATED JOBNAME POINTER => R2    25
         ICM   R5,B'1111',ASCBJBNI-ASCB(R4)                               26
         BNZ   INITIAT                 Â¬= 0; POINTER IS FOR INIT JOB      27
         L     5,ASCBJBNS-ASCB(4)      POINTER IS FOR TSO LOGON OR STC    28
INITIAT  DS    0H                                                         29
         MVC   PPGNAME(L'PPGNAME),0(R5) MOVE NAME TO PARMLIST             30
*        *---------------------------------------*                        31
*        *     DETERMINE LENGTH OF NAME          *                        32
*        *---------------------------------------*                        33
         LA    R1,8                    DEFAULT IF NO HIT ON TRT           34
*                                      SCAN THE NAME FOR X'40'            35
         TRT   PPGNAME(L'PPGNAME),TRTTBL                                  36
         BC    8,NOHIT                 NO SPACES IN NAME                  37
         LA    R5,PPGNAME             .. ADDRESS OF X'40'                 38
         SR    R1,R5                  .. SUBTRACTED FROM ADDRESS NAME     39
NOHIT    DS    0H                                                         40
         ST    R1,PPGNLEN               STORE LENGTH IN PARMLIST          41
         B     EXIT00                                                     42
*---------------------------------------------------------------------*   43
*        EXIT ROUTINES                                                *   44
*---------------------------------------------------------------------*   45
EXIT00   DS    0H                      SUCCESSFUL                         46
         LA    R15,X'00'                                                  47
         B     EXIT                                                       48
*---------------------------------------------------------------------*   49
*        COMMON EXIT                                                  *   50
*---------------------------------------------------------------------*   51
EXIT     DS   0H                                                          52
         ENDMOD                                                           53
*---------------------------------------------------------------------*   54
*        CONSTANTS                                                    *   55
*---------------------------------------------------------------------*   56
TRTTBL   DC    XL256'00'               TABLE FOR TRT                      57
         ORG   TRTTBL+X'40'                                               58
         DC    X'08'                                                      59
         ORG                                                              60
*---------------------------------------------------------------------*   61
*        WORK AREA                                                    *   62
*---------------------------------------------------------------------*   63
WPGMNAME DSECT                                                            64
LPGMNAME EQU   *-WPGMNAME                                                 65
*---------------------------------------------------------------------*   66
*        OTHER DSECTS                                                 *   67
*---------------------------------------------------------------------*   68
         PPGNM                         MAP INPUT PARMLIST                 69
         PRINT NOGEN                                                      70
         IHAPSA                        MAP PSA                            71
         IHAASCB                       MAP ASCB                           72
         END                                                              73
