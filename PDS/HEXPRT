*+--------------------------------------------------------------------+    1
*|  FUNCTION:                                                         |    2
*|    CONVERT HEX STRING TO HEX IN CHARACTER FORMAT.                  |    3
*|  INPUT:                                                            |    4
*|    R1 - ADDRESS OF 4 WORD PARMLIST                                 |    5
*|         1ST WORD - ADDRESS OF OUTPUT STRING                        |    6
*|         2ND WORD - LENGTH OF OUTPUT STRING                         |    7
*|         3RD WORD - ADDRESS OF INPUT STRING                         |    8
*|         4TH WORD - LENGTH OF INPUT STRING                          |    9
*|  OUTPUT:                                                           |   10
*|    R15 -RETURN CODE                                                |   11
*|          0 - INPUT STRING TRANSLATED TO OUTPUT STRING              |   12
*|          4 - OUTPUT AREA LONGER  THAN 2*INPUT (PADDING)            |   13
*|          8 - OUTPUT AREA SHORTER THAN 2*INPUT (TRUNCATION)         |   14
*|         12 - OUTPUT AREA NOT ALLOCATED BY CALLER.                  |   15
*|  LOGIC:                                                            |   16
*|    THE HIGH AND LOW ORDER 4 BITS OF EACH INPUT BYTE ARE            |   17
*|        PUT IN THE LOW ORDER 4 BITS OF 2 OUTPUT BYTES:              |   18
*|        INPUT:  |A5|37| ==> OUTPUT: |0A|05|03|07|                   |   19
*|                                                                    |   20
*|    THE TR INSTRUCTION IS USED TO TRANSLATE TO PRINTABLE HEX        |   21
*|        OUTPUT: |0A|05|03|07| ==>   |C1|F5|F3|F7|                   |   22
*|  ATTRIBUTES:                                                       |   23
*|    REENTRANT AMODE 31 RMODE ANY                                    |   24
*+--------------------------------------------------------------------+   25
*+--------------------------------------------------------------------+
*|  Copyright (c) 1992, 1993, 2002 by Mitchell Marx & Penelope Davis  |
*|                                                                    |
*|  Address questions to: mmarx@mdpaladin.com                         |
*|                                                                    |
*|  Permission is hereby granted, free of charge, to any person       |
*|  obtaining a copy of this software and associated documentation    |
*|  files (the "Software"), to deal in the Software without           |
*|  restriction, including without limitation the rights to use,      |
*|  copy, modify, merge, publish, distribute, sublicense, and/or sell |
*|  copies of the Software, and to permit persons to whom the         |
*|  Software is furnished to do so, subject to the following          |
*|  conditions:                                                       |
*|                                                                    |
*|  The above copyright notice and this permission notice shall be    |
*|  included in all copies or substantial portions of the Software.   |
*|                                                                    |
*|  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,   |
*|  EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES   |
*|  OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND          |
*|  NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT       |
*|  HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,      |
*|  WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING      |
*|  FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR     |
*|  OTHER DEALINGS IN THE SOFTWARE.                                   |
*+--------------------------------------------------------------------+
         MODULE HEXPRT,BASE=12,LOC=BELOW,AMODE=31,RMODE=ANY,           X  26
               TEXT='CONVERT HEX STRING TO PRINTABLE'                     27
         LR    R3,R1                   PARMLIST ADDRESS TO R3             28
         LM    R4,R7,0(R3)             LOAD PARMS                         29
*        *---------------------------------------*                        30
*        *     R4 = ADDRESS OF OUTPUT STRING     *                        31
*        *     R5 = LENGTH   OF OUTPUT STRING    *                        32
*        *     R6 = ADDRESS OF INPUT STRING      *                        33
*        *     R7 = LENGTH   OF INPUT STRING     *                        34
*        *---------------------------------------*                        35
*        *---------------------------------------*                        36
*        *     TEST IF OUTPUT IS ALLOCATED       *                        37
*        *---------------------------------------*                        38
         VSMLOC PVT,AREA=((R4),(R5)) .. OUTPUT ALLOCATED                  39
         LTR   R15,R15                 .. IN USER PRIVATE AREA ?          40
         BNZ   EXIT0C                 NO; EXIT                            41
*        *---------------------------------------*                        42
*        *     CLEAR OUTPUT STRING               *                        43
*        *---------------------------------------*                        44
         XR    R7,R7                  .. SET                              45
         ICM   R7,B'1000',=X'40'      .. OUTPUT                           46
         MVCL  R4,R6                  .. TO X'40'                         47
*        *---------------------------------------*                        48
*        *     INPUT TO OUTPUT AND TRANSLATE     *                        49
*        *---------------------------------------*                        50
         LM    R4,R7,0(R3)             LOAD PARMS                         51
HLOOP    DS    0H                                                         52
         CH    R5,=H'2'                AT LEAST 2 BYTES IN OUPUT ?        53
         BNL   MOREOP                  YES; CONTINUE                      54
         CH    R7,=H'1'                AT LEAST 1 BYTES IN INPUT ?        55
         BNL   EXIT08                  YES; EXIT INDICATING TRUNCATION    56
         B     EXIT00                  NO INPUT STRING/NO OUTPUT STRING   57
MOREOP   DS    0H                                                         58
         CH    R7,=H'1'                AT LEAST 1 BYTES IN INPUT ?        59
         BL    EXIT04                  NO; EXIT INDICATING PADDING        60
         ICM   R8,B'0010',0(R6)        INPUT BYTE TO REGISTER             61
         SRL   R8,4                    SHIFT HIGH NYBBLE TO LOW NYBBLE    62
         ICM   R8,B'0001',0(R6)        INPUT BYTE TO REGISTER             63
         N     R8,=A(X'00000F0F')      MASK OFF IRRELVANT BITS            64
         STCM  R8,B'0011',0(R4)        STORE 2 BYTES TO OUTPUT            65
*                                      TRANSLATE OUTPUT                   66
         TR    0(2,R4),=C'0123456789ABCDEF'                               67
         SH    R5,=H'2'                DECREMENT OUTPUT LENGTH            68
         LA    R4,2(R4)                BUMP IN OUTPUT STRING              69
         LA    R6,1(R6)                BUMP IN INPUT STRING               70
         BCTR  R7,0                    DECREMENT INPUT LENGTH             71
         B     HLOOP                   CHECK NEXT BYTES                   72
*---------------------------------------------------------------------*   73
*        EXIT ROUTINES                                                *   74
*---------------------------------------------------------------------*   75
EXIT00   DS    0H                      NO PADDING OR TRUNCATION           76
         LA    R15,X'00'                                                  77
         B     EXIT                                                       78
EXIT04   DS    0H                      OUTPUT STRING PADDED               79
         LA    R15,X'04'                                                  80
         B     EXIT                                                       81
EXIT08   DS    0H                      INPUT STRING TRUNCATED             82
         LA    R15,X'08'                                                  83
         B     EXIT                                                       84
EXIT0C   DS    0H                      OUTPUT STRING AREA NOT ALLOCATED   85
         LA    R15,X'0C'                                                  86
         B     EXIT                                                       87
*---------------------------------------------------------------------*   88
*        COMMON EXIT                                                  *   89
*---------------------------------------------------------------------*   90
EXIT     DS   0H                                                          91
         ENDMOD                                                           92
*---------------------------------------------------------------------*   93
*        WORK AREA                                                    *   94
*---------------------------------------------------------------------*   95
WHEXPRT  DSECT                                                            96
LHEXPRT  EQU   *-WHEXPRT                                                  97
         END                                                              98
