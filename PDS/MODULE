         MACRO                                                             1
         MODULE &CNAME,&AMODE=31,&RMODE=ANY,&SP=1,&LOC=,&BASE=12,      X   2
               &ENTRY=BRANCH,&FLOATSV=NO,&RENT=YES,&WORK=YES,          X   3
               &TEXT=' '                                                   4
.*+-------------------------------------------------------------------+
.*|  Copyright (c) 1992, 1993, 2002 by Mitchell Marx & Penelope Davis |
.*|                                                                   |
.*|  Address questions to: mmarx@mdpaladin.com                        |
.*|                                                                   |
.*|  Permission is hereby granted, free of charge, to any person      |
.*|  obtaining a copy of this software and associated documentation   |
.*|  files (the "Software"), to deal in the Software without          |
.*|  restriction, including without limitation the rights to use,     |
.*|  copy, modify, merge, publish, distribute, sublicense,and/or sell |
.*|  copies of the Software, and to permit persons to whom the        |
.*|  Software is furnished to do so, subject to the following         |
.*|  conditions:                                                      |
.*|                                                                   |
.*|  The above copyright notice and this permission notice shall be   |
.*|  included in all copies or substantial portions of the Software.  |
.*|                                                                   |
.*|  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,  |
.*|  EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES  |
.*|  OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND         |
.*|  NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT      |
.*|  HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,     |
.*|  WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING     |
.*|  FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR    |
.*|  OTHER DEALINGS IN THE SOFTWARE.                                  |
.*+-------------------------------------------------------------------+
.**********************************************************************    5
.*   BEGIN MODULE WITH THIS MACRO                                     *    6
.*     &CNAME   - NAME OF MODULE (7 CHARACTERS OR LESS)               *    7
.*     &AMODE   - "24" OR "31"                                        *    8
.*     &RMODE   - "24" OR "ANY"                                       *    9
.*     &SP      - SUBPOOL FOR RENTRANT STORAGE THIS MODULE            *   10
.*     &LOC     - "BELOW" IF GETMAINED STORAGE BELOW 16M              *   11
.*     &BASE    - BASE REGISTER (NOT R1, R2 OR R13)                   *   12
.*     &ENTRY   - "BRANCH" "PCSTACK" "PCBASIC" "SRB" OR "SVC"         *   13
.*     &FLOATSV - SAVE FLOATING POINT REGISTERS                       *   14
.*     &RENT    - WORK AREA DYNAMICALLY OBTAINED (DEFAULT = YES)      *   15
.*     &WORK    - WORK AREA TO BE GOTTEN (DEFAULT = YES)              *   16
.*     &TEXT    - COMMENT THAT IS ASSEMBLED INTO OBJECT CODE          *   17
.**********************************************************************   18
         GBLA  &POOL                                                      19
         GBLB  &EQU                                                       20
         GBLB  &CALSA,&WORKA,&FLOAT,&RENTA                                21
         GBLC  &SYSSPLV,&SYSASCE                                          22
         GBLC  &DSCTLEN,&NRENTWK,&DNAME                                   23
         GBLC  &SAVEGEN,&SAVELEN,&SAVEFLT,&SPLEVI                         24
         LCLB  &E1                                                        25
         LCLC  &BEGIN,&TXT,&ONSTACK                                       26
         SPLEVEL  TEST                                                    27
.*************************************************                        28
.*       CHECK ERROR CONDITIONS                  *                        29
.*************************************************                        30
&E1      SETB  0                                                          31
         AIF   (T'&CNAME NE 'O').OK1                                      32
         MNOTE 8,'MODULE NAME IS REQUIRED'                                33
&E1      SETB  1                                                          34
.OK1     ANOP                                                             35
         AIF   (K'&CNAME LE 7).OK2                                        36
         MNOTE 8,'MODULE NAME MUST BE LESS THAN 7 CHARACTERS'             37
&E1      SETB  1                                                          38
.OK2     ANOP                                                             39
         AIF   (&BASE GT 2 AND &BASE LT 13).OK3                           40
         MNOTE 8,'ONLY REGISTERS 3 THROUGH 12 CAN BE BASE REGISTERS'      41
&E1      SETB  1                                                          42
.OK3     ANOP                                                             43
         AIF   ('&ENTRY' EQ 'BRANCH' OR '&ENTRY' EQ 'PCSTACK').OK4        44
         AIF   ('&ENTRY' EQ 'PCBASIC' OR '&ENTRY' EQ 'SRB').OK4           45
         AIF   ('&ENTRY' EQ 'SVC').OK4                                    46
         MNOTE 8,'"ENTRY="  "BRANCH","PCSTACK","PCBASIC","SRB","SVC"'     47
&E1      SETB  1                                                          48
.OK4     ANOP                                                             49
         AIF   ('&ENTRY' NE 'PCSTACK' OR '&SYSSPLV' GT '2').OK5           50
         MNOTE 8,'"ENTRY=PCSTACK" MUST BE ASSEMBLED "SPLEVEL > 2"  '      51
&E1      SETB  1                                                          52
.OK5     ANOP                                                             53
         AIF   ('&ENTRY' NE 'SVC').OK6                                    54
         AIF   ('&BASE' EQ '6').OK6                                       55
         MNOTE 8,'BASE REGISTER MUST BE 6 WITH "ENTRY=SVC" '              56
&E1      SETB  1                                                          57
.OK6     ANOP                                                             58
         AIF   (&E1).MEND                                                 59
.*************************************************                        60
.*       SET GLOBAL SYMBOLS                      *                        61
.*************************************************                        62
&POOL    SETA  &SP                                                        63
&SPLEVI  SETC  '&SYSSPLV'                                                 64
&DSCTLEN SETC  'L'.'&CNAME'                                               65
&NRENTWK SETC  'N'.'&CNAME'                                               66
&DNAME   SETC  'W'.'&CNAME'                                               67
&BEGIN   SETC  'BEG'.'&SYSNDX'                                            68
&SAVEGEN SETC  'GEN'.'&SYSNDX'                                            69
&SAVEFLT SETC  'FLT'.'&SYSNDX'                                            70
&SAVELEN SETC  'LEN'.'&SYSNDX'                                            71
&ONSTACK SETC  'STK'.'&SYSNDX'                                            72
&TXT     SETC  '&TEXT'                                                    73
         AIF   ('&TXT'(1,1) EQ '''').STRIP                                74
         AIF   ('&TXT'(1,1) EQ '(').STRIP                                 75
         AGO   .NOSTRIP                                                   76
.STRIP   ANOP                                                             77
&TXT     SETC  '&TXT'(2,K'&TXT-2)                                         78
.NOSTRIP ANOP                                                             79
.*************************************************                        80
.*       SET GLOBAL SWITCHES                     *                        81
.*************************************************                        82
&WORKA   SETB  0     OBTAIN REENTRANT WORKAREA ?                          83
         AIF   ('&WORK'(1,1) EQ 'N').NWORK                                84
         AIF   ('&ENTRY' EQ 'PCBASIC' AND '&SPLEVI' LT '3').NWORK         85
         AIF   ('&ENTRY' EQ 'SVC' AND '&SPLEVI' LT '3').NWORK             86
&WORKA   SETB  1                                                          87
.NWORK   ANOP                                                             88
&RENTA   SETB  0     OBTAIN WORK AREA AND NON-REENTRANT                   89
         AIF   ('&RENT'(1,1) EQ 'N').NRENT                                90
         AIF   ('&ENTRY' EQ 'SRB' AND '&SPLEVI' LT '3').NRENT             91
&RENTA   SETB  1                                                          92
.NRENT   ANOP                                                             93
&CALSA   SETB  0     IS SAVE AREA PROVIDED BY CALLER ?                    94
         AIF   ('&ENTRY'(1,2) EQ 'PC').NSAVE                              95
         AIF   ('&ENTRY' EQ 'SRB').NSAVE                                  96
         AIF   ('&ENTRY' EQ 'SVC').NSAVE                                  97
&CALSA   SETB  1                                                          98
.NSAVE   ANOP                                                             99
&FLOAT   SETB  0     ARE FLOATING POINT REGISTERS SAVED ?                100
         AIF   ('&FLOATSV'(1,1) EQ 'N').NFLOT                            101
         AIF   (&WORKA EQ 0).NFLOT                                       102
&FLOAT   SETB  1                                                         103
.NFLOT   ANOP                                                            104
&CNAME   CSECT                                                           105
         AIF   ('&SPLEVI' LT '2').NOAMODE                                106
*        *---------------------------------------*                       107
*        *     SPECIFY AMODE AND RMODE           *                       108
*        *---------------------------------------*                       109
&CNAME   AMODE &AMODE                                                    110
&CNAME   RMODE &RMODE                                                    111
.NOAMODE ANOP                                                            112
         AIF   (&WORKA NE 1).NOWORK0                                     113
*        +---------------------------------------+                       114
*        |     SAVE AREAS                        |                       115
*        +---------------------------------------+                       116
&DNAME   DSECT                    USER ACQUIRED STORAGE FOR REENTRABLE   117
&SAVEGEN DS    18F                GENERAL REGISTER SAVE AREA             118
         AIF   (&FLOAT NE 1).NOF1                                        119
&SAVEFLT DS    4D                 FLOATING POINT REGISTER SAVE AREA      120
.NOF1    ANOP                                                            121
&SAVELEN DS    F                  LENGTH OF ACQUIRED STORAGE             122
&CNAME   CSECT                                                           123
.NOWORK0 ANOP                                                            124
         AIF   (&EQU).NOEQU                                              125
&EQU     SETB  1                                                         126
*        *---------------------------------------*                       127
*        *     REGISTER EQUATES                  *                       128
*        *---------------------------------------*                       129
         PRINT NOGEN                                                     130
         COPY EQUATES                                                    131
         PRINT GEN                                                       132
.NOEQU   ANOP                                                            133
*        *---------------------------------------*                       134
*        *     ENTRY POINT                       *                       135
*        *---------------------------------------*                       136
         AIF   ('&ENTRY'(1,2) EQ 'PC').PCENT                             137
         AIF   ('&ENTRY' EQ 'SVC').SVCENT                                138
.*************************************************                       139
.*       ENTRY = BRANCH AND SRB                  *                       140
.*************************************************                       141
         USING &CNAME,15          SET ADDRESSING ON ENTRY POINT          142
         B     &BEGIN             BRANCH AROUND EYECATCHER               143
         DC    C' &CNAME '        NAME OF CSECT                          144
         DC    C' &SYSDATE '      DATE OF ASSEMBLY                       145
         DC    C' &SYSTIME '      TIME OF ASSEMBLY                       146
         DC    C' &TXT  '                                                147
         AIF   ('&SPLEVI' LT '3').XA1                                    148
         AIF   (&WORKA EQ 0).NL1                                         149
&ONSTACK DC    C'F1SA'            CALLER'S SAVE AREA ON STACK            150
.NL1     ANOP                                                            151
&BEGIN   BAKR  14,0               PUSH REGS AND PSW ONTO STACK           152
         LAE   &BASE,0(15,0)      LOAD BASE REGISTER WITH ENTRYPNT       153
         MSTA  0                  PUSH REGS AND PSW ONTO STACK           154
         AGO   .DROP15            DROP ADDRESSING ON ENTRY POINT         155
.XA1     ANOP                                                            156
         AIF   (&CALSA NE 1).NOSA1                                       157
&BEGIN   SAVE  (14,12)            CALLER'S REGS => CALLER'S SAVEAREA     158
         LR    &BASE,15           LOAD BASE REGISTER WITH ENTRY POINT    159
         AGO   .DROP15            DROP ADDRESSING ON ENTRY POINT         160
.NOSA1   ANOP                                                            161
&BEGIN   LR    &BASE,15           LOAD BASE REGISTER WITH ENTRY POINT    162
.DROP15  ANOP                                                            163
         DROP  15                 DROP ADDRESSING ON ENTRY POINT         164
         USING &CNAME,&BASE       SET ADDRESSING ON BASE REGISTER        165
         AGO   .CKSTG                                                    166
.*************************************************                       167
.*       ENTRY = PCSTACK AND PCBASIC             *                       168
.*************************************************                       169
.PCENT   ANOP                                                            170
         BASR  &BASE,0            LOAD BASE REGISTER                     171
         BCTR  &BASE,0            .. SUBTRACT 2 FROM                     172
         BCTR  &BASE,0            .. BASE REGISTER                       173
         USING &CNAME,&BASE       SET ADDRESSING ON ENTRY POINT          174
         AIF   ('&SPLEVI' LT '3').PC2                                    175
         LAE   &BASE,0(&BASE,0)   CLEAR BASE ACCESS REGISTER             176
         MSTA  0                  SAVE REGS 1 AND 2 IN USER STACK        177
.PC2     ANOP                                                            178
         B     &BEGIN             BRANCH AROUND EYECATCHER               179
         DC    C' &CNAME '        NAME OF CSECT                          144
         DC    C' &SYSDATE '      DATE OF ASSEMBLY                       181
         DC    C' &SYSTIME '      TIME OF ASSEMBLY                       182
         DC    C' &TXT  '                                                183
         AIF   (&WORKA EQ 0).NL2                                         184
&ONSTACK DC    C'F1SA'            CALLER'S SAVE AREA ON STACK            185
.NL2     ANOP                                                            186
&BEGIN   DS    0H                                                        187
         AGO   .CKSTG                                                    188
.SVCENT  ANOP                                                            189
.*************************************************                       190
.*       ENTRY = SVC                             *                       191
.*************************************************                       192
         USING &CNAME,6           SET ADDRESSING ON ENTRY POINT          193
         B     &BEGIN             BRANCH AROUND EYECATCHER               194
         DC    C' &CNAME '        NAME OF CSECT                          195
         DC    C' &SYSDATE '      DATE OF ASSEMBLY                       196
         DC    C' &SYSTIME '      TIME OF ASSEMBLY                       197
         DC    C' &TXT  '                                                198
         AIF   ('&SPLEVI' LT '3').XA3                                    199
         AIF   (&WORKA EQ 0).NL3                                         200
&ONSTACK DC    C'F1SA'            CALLER'S SAVE AREA ON STACK            201
.NL3     ANOP                                                            202
&BEGIN   BAKR  14,0               PUSH REGS AND PSW ONTO STACK           203
         MSTA  0                  PUSH REGS AND PSW ONTO STACK           204
         AGO   .ESVCE             DROP ADDRESSING ON ENTRY POINT         205
.XA3     ANOP                                                            206
&BEGIN   DS    0H                                                        207
.ESVCE   ANOP                                                            208
.CKSTG   ANOP                                                            209
.*************************************************                       210
.*      GET A WORK AREA FOR REENTRANCY           *                       211
.*************************************************                       212
         AIF   (&WORKA EQ 0).NOWORK                                      213
         AIF   (&RENTA EQ 1).YESRENT                                     214
         LA    13,&NRENTWK        ADDRESS OF WORK AREA                   215
         AGO   .MAPDSECT                                                 216
.YESRENT ANOP                                                            217
*        *---------------------------------------*                       218
*        *     GET STORAGE FOR WORK AREA         *                       219
*        *---------------------------------------*                       220
         LA    0,&DSCTLEN          LENGTH OF STORAGE                     221
         AIF   (T'&LOC EQ 'O' OR '&SPLEVI' LT '2').NOLOC                 222
         AIF   ('&SPLEVI' LT '3').NOSTOR                                 223
         STORAGE OBTAIN,COND=NO,                                       X 224
               LENGTH=(0),                                             X 225
               LOC=&LOC,                                               X 226
               SP=&POOL,                                               X 227
               ADDR=(1)                                                  228
         AGO   .GOT                                                      229
.NOSTOR  ANOP                                                            230
         GETMAIN RU,                                                   X 231
               LV=(0),                                                 X 232
               LOC=&LOC,                                               X 233
               SP=&POOL                                                  234
         AGO   .GOT                                                      235
.NOLOC   ANOP                                                            236
         AIF   ('&SPLEVI' LT '3').NOSTOR1                                237
         STORAGE OBTAIN,COND=NO,                                       X 238
               LENGTH=(0),                                             X 239
               SP=&POOL,                                               X 240
               ADDR=(1)                                                  241
         AGO   .GOT                                                      242
.NOSTOR1 ANOP                                                            243
         GETMAIN RU,                                                   X 244
               LV=(0),                                                 X 245
               SP=&POOL                                                  246
.GOT     ANOP                                                            247
         AIF   ('&SPLEVI' GT '2').ESA2                                   248
         AIF   (&CALSA EQ 0).NOCSA0                                      249
         LR    2,13               PRESERVE ADDRESS CALLER'S SAVEAREA     250
         ST    13,4(1)            SAVE A(CALLER'S SAVE AREA)             251
         ST    1,8(13)            A(SAVE AREA) => CALLER'S SAVE AREA     252
.NOCSA0  ANOP                                                            253
         LR    13,1               A(SAVE AREA THIS CSECT) => R13         254
         AGO   .MAPDSECT                                                 255
.ESA2    ANOP                                                            256
         AIF   (&CALSA EQ 0).NOCSA                                       257
         ST    1,8(0,13)          A(SAVE AREA) => CALLER'S SAVE AREA     258
.NOCSA   ANOP                                                            259
         LAE   13,0(1,0)          LOAD SAVE AREA GEN AND ACC REGS        260
*                                 INDICATE CALLER'S SAVEAREA ON STACK    261
         MVC   4(L'&ONSTACK,13),&ONSTACK                                 262
.MAPDSECT ANOP                                                           263
         USING &DNAME,13          ADDRESSING FOR SAVE AREA               264
         ST    0,&SAVELEN         SAVE SUBPOOL AND LENGTH                265
.*************************************************                       266
.*       RESTORE REGISTERS FROM STACK OR         *                       267
.*       CALLER'S SAVE AREA                      *                       268
.*************************************************                       269
.NOWORK  ANOP                                                            270
         AIF   ('&SPLEVI' LT '3').XA2                                    271
         EREG  14,1               RESTORE REGS FROM STACK                272
         AGO   .NOCSA2                                                   273
.XA2     ANOP                                                            274
         AIF   (&CALSA EQ 0 OR &WORKA EQ 0).NOCSA2                       275
         LM    14,2,12(2)         RESTORE REGS FROM CALLER'S SAVEAREA    276
.NOCSA2  ANOP                                                            277
.******  SAVE FLOATING POINT REGISTERS ***********                       278
         AIF   (&FLOAT EQ 0).MEND                                        279
         STD   0,&SAVEFLT         ..SAVE                                 280
         STD   2,&SAVEFLT+8       ..FLOATING                             281
         STD   4,&SAVEFLT+16      ..POINT                                282
         STD   6,&SAVEFLT+24      ..REGISTERS                            283
.MEND    MEND                                                            284
