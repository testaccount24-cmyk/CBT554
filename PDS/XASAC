*+--------------------------------------------------------------------+    1
*|  FUNCTION:                                                         |    2
*|      CHAINS THROUGH TCBS AND CDES IN ANOTHER ADDRESS SPACE,        |    3
*|      MOVING MODULE NAMES TO A LIST IN CALLER'S ADDRESS SPACE.      |    4
*|  INPUTS:                                                           |    5
*|      R1 - PARMLIST MAPPED BY XMSXA MACRO.                          |    6
*|            CALLER HAS SET SECONDARY ADDRESS SPACE.                 |    7
*|  OUTPUTS:                                                          |    8
*|      MODULE NAMES INTO LIST SUPPLIED BY CALLER.                    |    9
*|      R15 - RETURN CODE                                             |   10
*|            0 - MODULE NAMES MOVED TO LIST                          |   11
*|  LOGIC:                                                            |   12
*|      1) GO INTO SECONDARY SPACE MODE.                              |   13
*|      2) CHAIN THRU TCBS AND CDES IN TARGET ADDRESS SPACE, WRITING  |   14
*|         MODULE NAMES TO LIST IN THIS ADDRESS SPACE.                |   15
*|      3) GO BACK TO PRIMARY SPACE MODE.                             |   16
*|                                                                    |   17
*|  ATTRIBUTES:                                                       |   18
*|      REENTERABLE, AMODE=31, RMODE=ANY, RUNS IN COMMON STORAGE      |   19
*+--------------------------------------------------------------------+   20
*+--------------------------------------------------------------------+
*|  Copyright (c) 1992, 1993, 2002 by Mitchell Marx & Penelope Davis  |
*|                                                                    |
*|  Address questions to: mmarx@mdpaladin.com                         |
*|                                                                    |
*|  Permission is hereby granted, free of charge, to any person       |
*|  obtaining a copy of this software and associated documentation    |
*|  files (the "Software"), to deal in the Software without           |
*|  restriction, including without limitation the rights to use,      |
*|  copy, modify, merge, publish, distribute, sublicense, and/or sell |
*|  copies of the Software, and to permit persons to whom the         |
*|  Software is furnished to do so, subject to the following          |
*|  conditions:                                                       |
*|                                                                    |
*|  The above copyright notice and this permission notice shall be    |
*|  included in all copies or substantial portions of the Software.   |
*|                                                                    |
*|  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,   |
*|  EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES   |
*|  OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND          |
*|  NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT       |
*|  HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,      |
*|  WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING      |
*|  FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR     |
*|  OTHER DEALINGS IN THE SOFTWARE.                                   |
*+--------------------------------------------------------------------+
         MODULE XASAC,BASE=12,LOC=BELOW,AMODE=31,RMODE=ANY,WORK=NO,    X  21
               TEXT='MOVE MODULE NAMES USING DAS'                         22
         LR    R3,R1                   ADDRESS OF PARMLIST TO R3          23
         USING XMSXA,R3                MAP PARMLIST                       24
         L     R7,XMMODS               ADDRESS OF TABLE OF MODULES        25
         LA    R7,12(R7)               POINT AT FIRST ENTRY               26
         L     R6,XMASCB               ADDRESS OF ASCB FROM PARMLIST      27
*        *---------------------------------------*                        28
*        *     GO INTO SECONDARY SPACE MODE      *                        29
*        *---------------------------------------*                        30
         SAC   256                     GO INTO SECONDARY SPACE MODE       31
*---------------------------------------------------------------------*   32
************  THE FOLLOWING CODE RUNS IN SECONDARY SPACE MODE  ********   33
*---------------------------------------------------------------------*   34
*        *---------------------------------------*                        35
*        *     CHAIN THRU TCBS IN TARGET ADDRESS *                        36
*        *     SPACE, WRITING MODULE NAMES TO    *                        37
*        *     TABLE IN PRIMARY ADDRESS SPACE    *                        38
*        *---------------------------------------*                        39
         XR    R9,R9                   CLEAR COUNTER                      40
*                                      ..IN PROBLEM STATE SO SET 3RD      41
         ICM   R4,B'0001',=X'80'       ..OPERAND TO MATCH PSW KEY MASK.   42
         L     R6,ASCBASXB-ASCB(,R6)   ADDR OF ASXB IN SECONDARY ASN      43
         L     R6,ASXBFTCB-ASXB(,R6)   ADDR OF 1ST TCB IN SECONDARY ASN   44
         USING TCB,R6                  MAP TCB                            45
T$CBLOOP DS    0H                                                         46
         ICM   R8,15,TCBJPQ            ADDRESS OF JOB-PACK-AREA QUEUE     47
         BZ    N$EXTCB                 =0; NO MODULES FOR THIS TCB        48
         USING CDENTRY,R8              MAP CDE                            49
C$DELOOP DS    0H                                                         50
         LA    R5,L'CDNAME             LENGTH OF MODULE NAME FOR MVCP     51
*                                      ..MOVE MODULE NAME FROM CDE        52
*                                      ..IN PRIMARAY ADDRESS SPACE TO     53
*                                      ..LIST IN SECONDARY ADDRESS SPC    54
         MVCP  0(R5,R7),CDNAME,R4                                         55
         LA    R9,1(R9)                INCREMENT COUNTER                  56
         LA    R7,L'CDNAME(,R7)        POINT AT NEXT ENTRY IN LIST        57
         ICM   R8,15,CDCHAIN           ADDRESS OF NEXT CDE ON JPAQ        58
         BNZ   C$DELOOP                ¬=0; PROCESS NEXT CDE              59
N$EXTCB  DS    0H                                                         60
         ICM   R6,15,TCBTCB            ADDRESS IF NEXT TCB ON TCB QUEUE   61
         BNZ   T$CBLOOP                ¬=0; POINT AT NEXT TCB             62
*---------------------------------------------------------------------*   63
************   END OF SECONDARY SPACE MODE CODE ***********************   64
*---------------------------------------------------------------------*   65
*        *---------------------------------------*                        66
*        *     GO INTO PRIMARY SPACE MODE        *                        67
*        *---------------------------------------*                        68
         SAC   0                       GO INTO PRIMARY   SPACE MODE       69
         L     R7,XMMODS               ..STORE NUMBER OF MODULE NAMES     70
         ST    R9,8(R7)                ..IN MODULE NAME LIST              71
         B     EXIT00                                                     72
*---------------------------------------------------------------------*   73
*        EXIT ROUTINES                                                *   74
*---------------------------------------------------------------------*   75
EXIT00   DS    0H                      SUCCESSFUL                         76
         LA    R15,X'00'                                                  77
         B     EXIT                                                       78
*---------------------------------------------------------------------*   79
*        COMMON EXIT                                                  *   80
*---------------------------------------------------------------------*   81
EXIT     DS    0H                                                         82
         ENDMOD                                                           83
*---------------------------------------------------------------------*   84
*        WORK AREA                                                    *   85
*---------------------------------------------------------------------*   86
WXASAC   DSECT                                                            87
LXASAC   EQU   *-WXASAC                                                   88
*---------------------------------------------------------------------*   89
*        OTHER DSECTS                                                 *   90
*---------------------------------------------------------------------*   91
         XMSXA                         USER PARMLIST                      92
         PRINT NOGEN                                                      93
         IHAASCB                      ADDRESS SPACE CONTROL BLOCK         94
         IHAASXB                      ADDRESS SPACE EXTENSION BLOCK       95
         IKJTCB                       TASK CONTROL BLOCK                  96
         IHACDE                       CONTENTS DIRECTORY ENTRY            97
         END                                                              98
