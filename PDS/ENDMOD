         MACRO                                                             1
         ENDMOD                                                            2
.*+-------------------------------------------------------------------+
.*|  Copyright (c) 1992, 1993, 2002 by Mitchell Marx & Penelope Davis |
.*|                                                                   |
.*|  Address questions to: mmarx@mdpaladin.com                        |
.*|                                                                   |
.*|  Permission is hereby granted, free of charge, to any person      |
.*|  obtaining a copy of this software and associated documentation   |
.*|  files (the "Software"), to deal in the Software without          |
.*|  restriction, including without limitation the rights to use,     |
.*|  copy, modify, merge, publish, distribute, sublicense,and/or sell |
.*|  copies of the Software, and to permit persons to whom the        |
.*|  Software is furnished to do so, subject to the following         |
.*|  conditions:                                                      |
.*|                                                                   |
.*|  The above copyright notice and this permission notice shall be   |
.*|  included in all copies or substantial portions of the Software.  |
.*|                                                                   |
.*|  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,  |
.*|  EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES  |
.*|  OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND         |
.*|  NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT      |
.*|  HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,     |
.*|  WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING     |
.*|  FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR    |
.*|  OTHER DEALINGS IN THE SOFTWARE.                                  |
.*+-------------------------------------------------------------------+
.*+-------------------------------------------------------------------+
.*|                     MAINTENANCE LOG                               |
.*+----------+------------------------------------+---------+---------+
.*|  DATE    |      DESCRIPTION                   | CHANGED | REFER   |
.*+----------+------------------------------------+---------+---------+
.*| 02/19/92 | USE REGISTER R1 INSTEAD OF R13     | MJM     | #01     |
.*|          |   IN STORAGE RELEASE ADDR= OPERAND |         |         |
.*|          |   (FIX#-R0001)                     |         |         |
.*+----------+------------------------------------+---------+---------+
.*                                                                    |
.**********************************************************************    3
.*       END MODULE WITH THIS MACRO                                   *    4
.**********************************************************************    5
         GBLA  &POOL                                                       6
         GBLB  &CALSA,&WORKA,&FLOAT,&RENTA                                 7
         GBLC  &SYSSPLV,&SYSASCE                                           8
         GBLC  &DSCTLEN,&NRENTWK,&DNAME                                    9
         GBLC  &SAVEGEN,&SAVELEN,&SAVEFLT,&SPLEVI                         10
         LCLC  &AROUND                                                    11
&AROUND  SETC  'AWK'.'&SYSNDX'                                            12
*---------------------------------------------------------------------*   13
*        COMMON EXIT                                                  *   14
*---------------------------------------------------------------------*   15
         AIF   (&FLOAT EQ 0).NOFLT                                        16
         LD    0,&SAVEFLT               .. RESTORE                        17
         LD    2,&SAVEFLT+8             ..FLOATING                        18
         LD    4,&SAVEFLT+16            ..POINT                           19
         LD    6,&SAVEFLT+24            ..REGISTERS                       20
.NOFLT   ANOP                                                             21
         AIF   (&WORKA EQ 0).NOWORK                                       22
         AIF   (&RENTA EQ 1).YRENT                                        23
         B     &AROUND                  BRANCH AROUND WORK AREA           24
         CNOP  0,8                      FORCE DOUBLEWORD ALIGNMENT        25
&NRENTWK DS    CL(&DSCTLEN)             WORK AREA                         26
&AROUND  DS    0H                                                         27
         AGO   .NOWORK                                                    28
.YRENT   ANOP                                                             29
         L     0,&SAVELEN               LENGTH OF STORAGE                 30
         LR    1,13                     A(SAVE AREA) => R2          #01   32
         AIF   ('&SPLEVI' GT '2').ESA1                              #01   31
         AIF   (&CALSA EQ 0).NOCSA                                        33
         L     13,&SAVEGEN+4            A(CALLER'S SAVE AREA)             34
*                                       FREE WORK AREA STORAGE            35
.NOCSA   ANOP                                                             36
         LR    2,15                                                       37
         FREEMAIN RU,                                                  X  38
               LV=(0),                                                 X  39
               SP=&POOL,                                               X  40
               A=(1)                                                      41
         LR    15,2                                                       42
         AGO   .NOWORK                                                    43
.ESA1    ANOP                                                             44
         LR    2,15                     RETURN CODE => R2                 45
*                                       FREE WORK AREA STORAGE            46
         STORAGE RELEASE,COND=NO,                                      X  47
               LENGTH=(0),                                             X  48
               ADDR=(1),                                            #01X  49
               SP=&POOL                                                   50
         LR    15,2                     RETURN CODE => R15                51
.NOWORK  ANOP                                                             52
         AIF   ('&SPLEVI' LT '3').NOESA1                                  53
         LA    0,3                      ..RESTORE REGS 0 AND 1            54
         ESTA  0,0                      ..FROM STACK MODIFIABLE AREA      55
         PR                             RESTORE REGS AND RETURN           56
         AGO   .MEND                                                      57
.NOESA1  ANOP                                                             58
         AIF   (&CALSA EQ 0).NOCSA1                                       59
         ST    15,16(,13)                                                 60
         LM    14,12,12(13)             RESTORE CALLER'S REGISTERS        61
.NOCSA1  ANOP                                                             62
         AIF   ('&SYSSPLV' EQ '1').NOXA1                                  63
         BSM   0,14                     RESTORE AMODE AND RETURN          64
         AGO   .MEND                                                      65
.NOXA1   ANOP                                                             66
         BR    14                       RETURN TO CALLER                  67
         AGO   .MEND                                                      68
*        *---------------------------------------*                        69
*        *     LITERALS                          *                        70
*        *---------------------------------------*                        71
.MEND    ANOP                                                             72
         LTORG                                                            73
         MEND                                                             74
