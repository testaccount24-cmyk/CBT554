*+--------------------------------------------------------------------+    1
*|  FUNCTION:                                                         |    2
*|      PRINTS NAMES OF ALL MODULES LOADED IN ANOTHER ADDRESS SPACE   |    3
*|  INPUTS:                                                           |    4
*|      NAME OF AN ADDRESS SPACE ON   // EXEC  PARM=                  |    5
*|  OUTPUTS:                                                          |    6
*|      MODULE NAME LIST TO OUTPUT FILE.                              |    7
*|      R15 - RETURN CODE                                             |    8
*|             0 - MODULE NAMES WRITTEN TO OUTPUT FILE.               |    9
*|             8 - TARGET ADDRESS SPACE NAME NOT ON PARM= .           |   10
*|            12 - GETASCB ROUTINE NOT LINK-EDITED.                   |   11
*|            16 - GETASCB ROUTINE ERROR.                             |   12
*|            20 - ERROR IN LOAD OF SECONDARY SPACE ROUTINE.          |   13
*|            24 - ERROR IN SECONDARY SPACE ROUTINE.                  |   14
*|            28 - PRINT ROUTINE NOT LINK-EDITED.                     |   15
*|            28 - ERROR IN PRINT ROUTINE                             |   16
*|  PROGRAM LOGIC:                                                    |   17
*|      1) PUT ADDRESS SPACE NAME FROM // EXEC PARM= INTO PARMLIST.   |   18
*|      2) CALL GETASCB TO SCAN ASVT FOR ADDRESS OF TARGET ASCB.      |   19
*|      3) LOAD SECONDARY SPACE MODULE INTO PAGEABLE CSA.             |   20
*|      4) USING ASN FROM TARGET ASCB, SET TARGET AS SECONDARY SPACE. |   21
*|      5) BRANCH INTO MODULE LOADED INTO CSA IN STEP 3 .             |   22
*|         THIS CODE MOVES THE NAMES OF MODULES IN THE TARGET         |   23
*|         ADDRESS SPACE TO A LIST IN THIS ADDRESS SPACE.             |   24
*|      6) RESTORE ORIGINAL SASN AND AX.                              |   25
*|      7) INVOKE MODULE TO PRINT MODULE NAME LIST.                   |   26
*|  MODULES INVOKED:                                                  |   27
*|      GETASCB, XASAC, PRTMOD                                        |   28
*|  ATTRIBUTES:                                                       |   29
*|      REENTERABLE, AMODE=31, RMODE=24, APF-AUTHORIZED               |   30
*+--------------------------------------------------------------------+   31
*+--------------------------------------------------------------------+
*|  Copyright (c) 1992, 1993, 2002 by Mitchell Marx & Penelope Davis  |
*|                                                                    |
*|  Address questions to: mmarx@mdpaladin.com                         |
*|                                                                    |
*|  Permission is hereby granted, free of charge, to any person       |
*|  obtaining a copy of this software and associated documentation    |
*|  files (the "Software"), to deal in the Software without           |
*|  restriction, including without limitation the rights to use,      |
*|  copy, modify, merge, publish, distribute, sublicense, and/or sell |
*|  copies of the Software, and to permit persons to whom the         |
*|  Software is furnished to do so, subject to the following          |
*|  conditions:                                                       |
*|                                                                    |
*|  The above copyright notice and this permission notice shall be    |
*|  included in all copies or substantial portions of the Software.   |
*|                                                                    |
*|  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,   |
*|  EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES   |
*|  OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND          |
*|  NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT       |
*|  HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,      |
*|  WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING      |
*|  FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR     |
*|  OTHER DEALINGS IN THE SOFTWARE.                                   |
*+--------------------------------------------------------------------+
         MODULE XAMOVE,BASE=12,LOC=BELOW,AMODE=31,RMODE=ANY,           X  32
               TEXT='MODULE NAMES FROM ANOTHER ADDRESS SPACE'             33
*        *---------------------------------------*                        34
*        *   GET ADDRESS SPACE NAME AND LENGTH   *                        35
*        *   FROM PARM= ON THE // EXEC CARD      *                        36
*        *---------------------------------------*                        37
         LA    R3,PARMLIST        ADDRESS OF PARMLIST TO R3               38
         USING XMSXA,R3           MAP PARMLIST                            39
         MVI   XMNAME,X'40'                  ..SET MODULE NAME IN         40
         MVC   XMNAME+1(L'XMNAME-1),XMNAME   ..PARMLIST TO SPACES         41
         L     R1,0(R1)           ADDRESS OF PARM= STRING TO R1           42
         XR    R2,R2                                                      43
         ICM   R2,B'0011',0(R1) LENGTH OF PARM                            44
         BZ    EXIT08             =0; PROGRAM NAME NOT GIVEN              45
         ST    R2,XMNAMEL         SAVE IN PLIST                           46
         BCTR  R2,0               LESS 1 FOR EXECUTE                      47
         EX    R2,MVIT1           MOVE NAME TO PLIST                      48
         B     MVIT1A             BRANCH AROUND EXECUTED INSTRUCTION      49
MVIT1    MVC   XMNAME(0),2(R1)    ** EXECUTE ONLY **                      50
MVIT1A   DS    0H                                                         51
         MVC   MODLIST(L'XMNAME),XMNAME  MOVE ASCB NAME TO MODNAME LIST   52
*        *---------------------------------------*                        53
*        *   CALL SCAN OF ASVT TO OBTAIN A(ASCB) *                        54
*        *---------------------------------------*                        55
         LR    R1,R3                   PARMLIST                           56
         ICM   R15,15,=V(GETASCB)      MODULE ADDRESS TO REG 15           57
         BZ    EXIT0C                  =0; ROUTINE NOT LINKED             58
         O     R15,=A(X'80000000')     SET FOR ENTRY AMODE 31             59
         BASSM R14,R15                 INVOKE THE ROUTINE.                60
         LTR   R15,R15                 WAS ADDRESS SPACE FOUND?           61
         BNZ   EXIT10                  NO; EXIT                           62
*        *---------------------------------------*                        63
*        *     LOAD MODULE THAT WILL RUN IN      *                        64
*        *     SECONDARY SPACE MODE INTO CSA     *                        65
*        *---------------------------------------*                        66
         MODESET MODE=SUP         GET SUPERVISOR STATE PSW                67
         LOAD  EP=XASAC,                                               X  68
               GLOBAL=(YES,P),    LOAD INTO PAGEABLE CSA               X  69
               ERRET=EXIT14,      ERROR ROUTINE                        X  70
               EOM=NO             DELETE MODULE AT END-OF-TASK            71
         ST    R0,ADDRCSA         SAVE ADDRESS OF ROUTINE                 72
*        *---------------------------------------*                        73
*        *     SET SECONDARY ASN TO TARGET       *                        74
*        *     ADDRESS SPACE                     *                        75
*        *---------------------------------------*                        76
         LA    R0,1               .. GET AX                               77
         AXSET AX=(R0)            .. OF 1                                 78
         ST    R0,XMAXOL          SAVE THE OLD AX                         79
         MODESET MODE=PROB        RETURN TO PROBLEM STATE                 80
         ESAR  R2                 EXTRACT SASN                            81
         ST    R2,XMSASNOL        SAVE THE OLD SASN                       82
         L     R2,XMASCB          ASCB OF TARGET ADDRESS SPACE            83
*                                 GET ADDRESS SPACE NUMBER FROM ASCB      84
         LH    R2,ASCBASID-ASCB(R2)                                       85
         SSAR  R2                 SET AS SECONDARY ADDRESS SPACE          86
*        *---------------------------------------*                        87
*        *     INVOKE TCB SCAN ROUTINE THAT      *                        88
*        *     RUNS IN SECONDARY SPACE MODE      *                        89
*        *---------------------------------------*                        90
         L     R15,ADDRCSA        ENTRY POINT OF ROUTINE TO R15           91
         LA    R1,MODLIST         ..ADDRESS OF MODULES LIST               92
         ST    R1,XMMODS          ..TO PARMLIST                           93
         LA    R1,PARMLIST        ADDRESS OF PARMLIST TO R1               94
         BASSM R14,R15            INVOKE THE ROUTINE                      95
         LTR   R15,R15            RC = 0 ?                                96
         BNZ   EXIT18             NO; EXIT WITH ERROR                     97
*        *---------------------------------------*                        98
*        *     RESTORE ORIGINAL SASN AND AX      *                        99
*        *---------------------------------------*                       100
         L     R2,XMSASNOL        .. RESTORE                             101
         SSAR  R2                 .. ORIGINAL SASN                       102
         MODESET MODE=SUP         GET A SUPERVISOR STATE PSW             103
         L     R0,XMAXOL          .. RESTORE                             104
         AXSET AX=(R0)            .. OLD AX                              105
         MODESET MODE=PROB        RETURN TO PROBLEM STATE                106
         DELETE EP=XASAC          DELETE THE ROUTINE FROM CSA            107
*        *---------------------------------------*                       108
*        *     PRINT LIST OF MODULE NAMES        *                       109
*        *---------------------------------------*                       110
         LA    R1,MODLIST          LIST OF MODULE NAMES                  111
         ICM   R15,15,=V(PRTMOD)   ADDRESS OF PRINT MODULE               112
         BZ    EXIT1C              =0? ; EXIT                            113
         BASSM R14,R15             INVOKE ROUTINE                        114
         LTR   R15,R15             RC = 0 ?                              115
         BNZ   EXIT20              NO; EXIT                              116
         B     EXIT00                                                    117
*---------------------------------------------------------------------*  118
*        EXIT ROUTINES                                                *  119
*---------------------------------------------------------------------*  120
EXIT00   DS    0H                      SUCCESSFUL                        121
         LA    R15,X'00'                                                 122
         B     EXIT                                                      123
EXIT08   DS    0H             ADDRESS SPACE NAME NOT IN PARM=            124
         LA    R15,X'08'                                                 125
         B     EXIT                                                      126
EXIT0C   DS    0H                      GET ASCB ROUTINE NOT LINKED       127
         LA    R15,X'0C'                                                 128
         B     EXIT                                                      129
EXIT10   DS    0H                      GETASCB DID NOT FIND TARGET ASID  130
         LA    R15,X'10'                                                 131
         B     EXIT                                                      132
EXIT14   DS    0H                      ERROR IN LOAD OF MODULE TO CSA    133
         PERCRC RC=X'14'                                                 134
         B     EXIT                                                      135
EXIT18   DS    0H                      ERROR IN SECONDARY SPACE RTN      136
         PERCRC RC=X'18'                                                 137
         B     EXIT                                                      138
EXIT1C   DS    0H                      PRINT ROUTINE NOT LINK EDITED     139
         LA    R15,X'1C'                                                 140
         B     EXIT                                                      141
EXIT20   DS    0H                      ERROR IN PRINT ROUTINE            142
         LA    R15,X'20'                                                 143
         B     EXIT                                                      144
*---------------------------------------------------------------------*  145
*        COMMON EXIT                                                  *  146
*---------------------------------------------------------------------*  147
EXIT     DS    0H                                                        148
         MODESET MODE=PROB        RETURN TO PROBLEM STATE                149
         ENDMOD                                                          150
*---------------------------------------------------------------------*  151
*        WORK AREA                                                    *  152
*---------------------------------------------------------------------*  153
WXAMOVE  DSECT                                                           154
ADDRCSA  DS    F                   ADDRESS OF SS MODE ROUTINE            155
MODMAX   EQU   500                 MAXIMUM ENTRIES IN MODULE LIST        156
PARMLIST DS    0F,CL(XMSXAL)       PARMLIST FOR XMSXA                    157
MODLIST  DS    CL8,F,CL(MODMAX)    LIST OF MODULE NAMES                  158
MODCOUNT EQU   MODLIST+8,4         COUNT OF MODULE NAMES IN LIST         159
MODNAMES EQU   MODLIST+12,8        FIRST MODULE NAME IN LIST             160
LXAMOVE  EQU    *-WXAMOVE                                                161
*---------------------------------------------------------------------*  162
*        OTHER DSECTS                                                 *  163
*---------------------------------------------------------------------*  164
         XMSXA                     MAP USER PARMLIST                     165
         IHAASCB LIST=NO           ADDRESS SPACE CONTROL BLOCK (ASCB)    166
         END                                                             167
