*+--------------------------------------------------------------------+    1
*|  FUNCTION:                                                         |    2
*|     PRINT ROUTINE FOR XMS MODULE NAME PROGRAMS.                    |    3
*|  INPUTS:                                                           |    4
*|     R1 - ADDRESS OF MODULE NAME LIST.  FIRST FULLWORD              |    5
*|          WORD IS THE NUMBER OF ENTRIES IN THE LIST.                |    6
*|  OUTPUTS:                                                          |    7
*|     R15 - RETURN CODE                                              |    8
*|           00 - FOUND                                               |    9
*|  PROGRAM LOGIC:                                                    |   10
*|      1) LOOP THROUGH MODULE LIST TABLE, PRINTING EACH ENTRY.       |   11
*|  ATTRIBUTES:                                                       |   12
*|      REENTERABLE, AMODE=24, RMODE=24                               |   13
*+--------------------------------------------------------------------+   14
*+--------------------------------------------------------------------+
*|  Copyright (c) 1992, 1993, 2002 by Mitchell Marx & Penelope Davis  |
*|                                                                    |
*|  Address questions to: mmarx@mdpaladin.com                         |
*|                                                                    |
*|  Permission is hereby granted, free of charge, to any person       |
*|  obtaining a copy of this software and associated documentation    |
*|  files (the "Software"), to deal in the Software without           |
*|  restriction, including without limitation the rights to use,      |
*|  copy, modify, merge, publish, distribute, sublicense, and/or sell |
*|  copies of the Software, and to permit persons to whom the         |
*|  Software is furnished to do so, subject to the following          |
*|  conditions:                                                       |
*|                                                                    |
*|  The above copyright notice and this permission notice shall be    |
*|  included in all copies or substantial portions of the Software.   |
*|                                                                    |
*|  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,   |
*|  EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES   |
*|  OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND          |
*|  NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT       |
*|  HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,      |
*|  WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING      |
*|  FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR     |
*|  OTHER DEALINGS IN THE SOFTWARE.                                   |
*+--------------------------------------------------------------------+
         MODULE PRTMOD,BASE=12,LOC=BELOW,AMODE=24,RMODE=24,            X  15
               TEXT='PRINT MODULE NAME LIST'                              16
         LA    R3,0(R1)                ASCB NAME                          17
         L     R9,8(R1)                NUMBER OF MODULES IN LIST          18
         LA    R7,12(R1)               POINT TO FIRST ENTRY IN LIST       19
         MVC   PRINT(L'PRINT),MPRINT   COPY DCB TO WORK AREA              20
         MVC   OLIST(L'OLIST),MOLIST   COPY OPEN/CLOSE LIST TO WORK       21
         OPEN  (PRINT,OUTPUT),MF=(E,OLIST) OPEN PRINT LIST                22
         MVI   OUTLINE,X'40'           MOVE A SPACE TO OUTLINE            23
         MVC   OUTLINE+1(L'OUTLINE-1),OUTLINE  PROPAGATE SPACES           24
         MVC   MPRT1(L'H1),H1          MOVE FIRST HEADING TO PRINT        25
         MVC   MPRT1+L'H1-8(8),0(R3)   MOVE ASCB NAME TO PRINT LINE       26
         PUT   PRINT,OUTLINE           WRITE FIRST RECORD                 27
PRLOOP   DS    0H                                                         28
         MVI   OUTLINE,X'40'           MOVE A SPACE TO OUTLINE            29
         MVC   OUTLINE+1(L'OUTLINE-1),OUTLINE  PROPAGATE SPACES           30
         MVC   MPRT1(L'MPRT1),0(R7)    MOVE MODULE TO PRINT               31
         PUT   PRINT,OUTLINE           WRITE MODULE NAME                  32
         LA    R7,8(R7)                POINT TO NEXT MODNAME IN LIST      33
         BCT   R9,PRLOOP               LOOP THRU NAMES                    34
         CLOSE MF=(E,OLIST)            CLOSE THE PRINT FILE               35
*---------------------------------------------------------------------*   36
*        EXIT ROUTINES                                                *   37
*---------------------------------------------------------------------*   38
EXIT00   DS    0H                      SUCCESSFUL                         39
         LA    R15,X'00'                                                  40
         B     EXIT                                                       41
*---------------------------------------------------------------------*   42
*        COMMON EXIT                                                  *   43
*---------------------------------------------------------------------*   44
EXIT     DS    0H                                                         45
         ENDMOD                                                           46
*---------------------------------------------------------------------*   47
*        CONSTANTS                                                    *   48
*---------------------------------------------------------------------*   49
H1       DC   C'MODULES IN ADDRESS SPACE         '                        50
*---------------------------------------------------------------------*   51
*        DCB AND OPEN/CLOSE PARMLIST                                  *   52
*---------------------------------------------------------------------*   53
LRECL    EQU   133                                                        54
BLKSIZE  EQU   LRECL*10                                                   55
MPRINT   DCB   DDNAME=PRINT,DSORG=PS,MACRF=PM,RECFM=FBA,LRECL=LRECL,   X  56
               BLKSIZE=BLKSIZE                                            57
LPRINT   EQU   *-MPRINT                                                   58
MOLIST   OPEN  (,),MF=L                                                   59
LOLIST   EQU   *-MOLIST                                                   60
*---------------------------------------------------------------------*   61
*        WORK AREA                                                    *   62
*---------------------------------------------------------------------*   63
WPRTMOD  DSECT                                                            64
MODMAX   EQU   500                     MAXIMUM ENTRIES IN MODULE LIST     65
PRINT    DS    CL(LPRINT)              PRINT DCB                          66
OLIST    DS    CL(LOLIST)              LIST FOR OPEN                      67
OUTLINE  DS    CL(LRECL)               OUTPUT LINE                        68
MPRT1    EQU   OUTLINE+5,8             FIRST OUTPUT FIELD                 69
LPRTMOD  EQU   *-WPRTMOD                                                  70
         END                                                              71
