*+--------------------------------------------------------------------+    1
*| FUNCTION:                                                          |    2
*|   ATTACHES A SUBTASK AND SCHEDULES AN IRB ROUTINE TO THE SUBTASK.  |    3
*|   . SINCE THIS PROGRAM ISSUES NO SVCS, IT COULD BE CALLED FROM     |    4
*|     AN SRB ROUTINE.  BRANCH-ENTRY INTO THE STAGE 1 AND 2 EXIT      |    5
*|     EFFECTORS REQUIRES THAT THE LOCAL LOCK BE OBTAINED.            |    6
*| LOGIC:                                                             |    7
*|   1) ATTACH A NON-DISPATCHABLE SUBTASK.                            |    8
*|   2) LOAD THE IRB ROUTINE.                                         |    9
*|   3) GET THE LOCAL LOCK.                                           |   10
*|   4) ISSUE CIRB TO CREATE AN IRB AND OBTAIN AN IQE WORK AREA.      |   11
*|      THIS IS THE STAGE 1 EXIT EFFECTOR                             |   12
*|   5) INITIALIZE THE IQE SO THAT IT CONTAINS IRB AND TCB ADDRESSES. |   13
*|   6) INVOKE CVT0EF00, THE STAGE 2 EXIT EFFECTOR.                   |   14
*|   7) RELEASE THE LOCAL LOCK.                                       |   15
*|   8) MAKE THE SUBTASK DISPATCHABLE.                                |   16
*|   9) WAIT UNTIL POSTED BY THE SUBTASK.                             |   17
*|  10) DETACH THE SUBTASK.                                           |   18
*| INPUTS:                                                            |   19
*|   NONE                                                             |   20
*| OUTPUTS:                                                           |   21
*|   CONSOLE MESSAGES                                                 |   22
*| ATTRIBUTES:                                                        |   23
*|   AMODE 24, RMODE 24, REENTRANT, REQUIRES APF AUTHORIZATION        |   24
*+--------------------------------------------------------------------+   25
*+--------------------------------------------------------------------+
*|  Copyright (c) 1992, 1993, 2002 by Mitchell Marx & Penelope Davis  |
*|                                                                    |
*|  Address questions to: mmarx@mdpaladin.com                         |
*|                                                                    |
*|  Permission is hereby granted, free of charge, to any person       |
*|  obtaining a copy of this software and associated documentation    |
*|  files (the "Software"), to deal in the Software without           |
*|  restriction, including without limitation the rights to use,      |
*|  copy, modify, merge, publish, distribute, sublicense, and/or sell |
*|  copies of the Software, and to permit persons to whom the         |
*|  Software is furnished to do so, subject to the following          |
*|  conditions:                                                       |
*|                                                                    |
*|  The above copyright notice and this permission notice shall be    |
*|  included in all copies or substantial portions of the Software.   |
*|                                                                    |
*|  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,   |
*|  EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES   |
*|  OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND          |
*|  NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT       |
*|  HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,      |
*|  WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING      |
*|  FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR     |
*|  OTHER DEALINGS IN THE SOFTWARE.                                   |
*+--------------------------------------------------------------------+
         MODULE BLDIRB,BASE=12,LOC=BELOW,AMODE=24,RMODE=24,            X  26
               TEXT=' ',SP=0                                              27
*        *---------------------------------------*                        28
*        *     ATTACH THE SUBTASK                *                        29
*        *---------------------------------------*                        30
         LA    R3,MAINECB              ADDR ECB THIS TASK                 31
         ATTACH EP=SUBTSK,             ATTACH SUBTASK                  X  32
               ECB=(R3),               WTTACH SUBTASK                  X  33
               DISP=NO,                NON-DISPATCHABLE                X  34
               SF=(E,ATTLIST)          PARMLIST FOR ATTACH                35
         ST    R1,ADDRTCB              SAVE SUBTASK TCB ADDRESS           36
         WTO   'SUBTASK ATTACHED'                                         37
*        *---------------------------------------*                        38
*        *     LOAD THE IRB ROUTINE              *                        39
*        *---------------------------------------*                        40
         LOAD  EP=IRBRTN,ERRET=EXIT08 LOAD THE IRB ROUTINE                41
         LR    R3,R0                                                      42
*        *---------------------------------------*                        43
*        *     GET THE LOCAL LOCK                *                        44
*        *---------------------------------------*                        45
         MODESET KEY=ZERO,MODE=SUP     GET A SUPERVISOR/KEY ZERO PSW      46
         SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,                        X  47
               REGS=USE,RELATED=REQUIRED                                  48
*        *---------------------------------------*                        49
*        *     CREATE AN IRB                     *                        50
*        *---------------------------------------*                        51
         L     R4,ADDRTCB              LOAD ADDR OF SUBTASK TCB           52
         CIRB  EP=(R3),                ENTRY POINT OF IRB ROUTINE      X  53
               KEY=PP,MODE=PP,         IRB RTN RUNS IN PROB STAT/TCBKEYX  54
               SVAREA=YES,             SAVE AREA FOR IRB ROUTINE       X  55
               RETIQE=NO,              GET AN IQE, NOT AN RQE          X  56
               STAB=(DYN),             FREE THE IRB AT EXIT            X  57
               WKAREA=10,              80 BYTES OF WORK AREA           X  58
               BRANCH=YES              BRANCH ENTER THE CIRB ROUTINE      59
         LR    R3,R1                   IRB ADDRESS TO R3                  60
         USING RBBASIC,R3              MAP THE IRB                        61
*        *---------------------------------------*                        62
*        *     INITIALIZE THE IQE                *                        63
*        *---------------------------------------*                        64
         L     R5,RBNEXAV              ADDRESS OF THE IQE WORK AREA       65
         USING IQESECT,R5              MAP THE IQE                        66
         ST    R3,IQEIRB               IRB ADDRESS TO IQE                 67
         L     R4,ADDRTCB              LOAD ADDR OF SUBTASK TCB           68
         ST    R4,IQETCB               TARGET TCB ADDRESS TO IQE          69
*        *---------------------------------------*                        70
*        *     INVOKE THE STAGE 2 EXIT EFFECTOR. *                        71
*        *---------------------------------------*                        72
         LCR   R1,R5                   COMPLEMENT IQE ADDR => R1          73
         L     R15,CVTPTR              ADDRESS OF THE CVT                 74
         L     R15,CVT0EF00-CVT(R15)   ADDRESS OF STAGE 2 ROUTINE         75
         BALR  R14,R15                 CALL THE ROUTINE                   76
*        *---------------------------------------*                        77
*        *     RELEASE THE LOCAL LOCK            *                        78
*        *---------------------------------------*                        79
         SETLOCK RELEASE,TYPE=ALL,                                     X  80
               REGS=USE,RELATED=REQUIRED                                  81
         MODESET KEY=NZERO,MODE=PROB   GET A SUPERVISOR/KEY ZERO PSW      82
*        *---------------------------------------*                        83
*        *     MAKE THE SUBTASK DISPATCHABLE     *                        84
*        *---------------------------------------*                        85
         LA    R1,ADDRTCB              LOAD SUBTASK TCB ADDRESS           86
         STATUS START,TCB=(R1)         MAKE THE SUBTASK DISPATCHABLE      87
*        *---------------------------------------*                        88
*        *     WAIT UNTIL POSTED BY SUBTASK      *                        89
*        *---------------------------------------*                        90
         LA    R1,MAINECB              ..WAIT UNTIL                       91
         WAIT  ECB=(R1)                ..POSTED BY SUBTASK                92
         WTO   'MAIN TASK POSTED BY SUBTASK'                              93
*        *---------------------------------------*                        94
*        *     DETACH THE SUBTASK                *                        95
*        *---------------------------------------*                        96
         LA    R1,ADDRTCB              LOAD ADDR OF SUBTASK TCB ADDR      97
         DETACH (R1)                   DETACH THE SUBTASK                 98
         WTO   'SUBTASK DETACHED'                                         99
         B     EXIT00                                                    100
*---------------------------------------------------------------------*  101
*        EXIT ROUTINES                                                *  102
*---------------------------------------------------------------------*  103
EXIT00   DS    0H                      SUCCESSFUL                        104
         LA    15,X'00'                                                  105
         B     EXIT                                                      106
EXIT08   DS    0H                      LOAD FOR IRB ROUTINE FAILED       107
         LA    15,X'08'                                                  108
         B     EXIT                                                      109
*---------------------------------------------------------------------*  110
*        COMMON EXIT                                                  *  111
*---------------------------------------------------------------------*  112
EXIT     DS   0H                                                         113
         ENDMOD             RESTORE REGISTERS AND RETURN                 114
*---------------------------------------------------------------------*  115
*        WORK AREA                                                    *  116
*---------------------------------------------------------------------*  117
WBLDIRB  DSECT                                                           118
ATTLIST  ATTACH ,SF=L                  PARMLIST FOR ATTACH               119
ADDRTCB  DS    A                       ADDRESS OF NEW TCB FROM ATTACH    120
MAINECB  DS    F                       ECB FOR THIS TASK                 121
LBLDIRB  EQU    *-WBLDIRB                                                122
*---------------------------------------------------------------------*  123
*        OTHER DSECTS                                                 *  124
*---------------------------------------------------------------------*  125
         CVT    DSECT=YES,LIST=NO      MAP THE CVT                       126
         IHAPSA DSECT=YES,LIST=NO      MAP THE PSA                       127
         IHAIQE                        MAP THE IQE                       128
         IHARB                         MAP THE RB                        129
         END                                                             130
