*+--------------------------------------------------------------------+
*|                      MAINTENANCE LOG                               |
*+----------+-------------------------------------+---------+---------+
*|  DATE    |       DESCRIPTION                   | CHANGED | REFER   |
*+----------+-------------------------------------+---------+---------+
*| 03/21/92 |  ALET FOR DSPACE 2 TO ACCESS REG    | MJM     | @01     |
*|          |    (FIX#-R0003)                     |         |         |
*+----------+-------------------------------------+---------+---------+
*                                                                     |
*+--------------------------------------------------------------------+    1
*| FUNCTION:                                                          |    2
*|   DSPUT IS A TEST PROGRAM FOR THE DATASPACES CREATED BY DSPACE.    |    3
*|   AFTER DSPACE IS STARTED AT THE CONSOLE, THE OPERATOR ISSUES:     |    4
*|       F DSPACE,CREATE(DSP1) - TO CREATE A DATASPACE NAMED "DSP1"   |    5
*|       F DSPACE,CREATE(DSP2) - TO CREATE A DATASPACE NAMED "DSP2" . |    6
*|   WHEN DSPUT IS NOW EXECUTED, IT WILL MOVE A WTO PARAMETER LIST    |    7
*|   FROM THIS ADDRESS SPACE TO DATASPACE DSP1, FROM DSP1 TO DSP2,    |    8
*|   AND FROM DSP2 TO A LOCATION IN THIS ADDRESS SPACE.  DSPUT THEN   |    9
*|   WRITES A CONSOLE MESSAGE BY ISSUING WTO MF=E USING THE PARMLIST. |   10
*| INPUTS:                                                            |   11
*|   NONE                                                             |   12
*| OUTPUTS:                                                           |   13
*|   R15 - RETURN CODE                                                |   14
*|          0 - SUCCESSFUL COMPLETION                                 |   15
*|          8 - DSPFIND ROUTINE NOT LINK EDITED                       |   16
*|         16 - DSPFIND DID NOT LOCATE DSP1 OR DSP2                   |   17
*| LOGIC:                                                             |   18
*|    1) PREPARE TO ACCEPT COMMANDS FROM THE MVS CONSOLE.             |   19
*| ATTRIBUTES:                                                        |   20
*|    AMODE 31, RMODE ANY, REENTRANT, REQUIRES APF AUTHORIZATION      |   21
*+--------------------------------------------------------------------+   22
*+--------------------------------------------------------------------+
*|  Copyright (c) 1992, 1993, 2002 by Mitchell Marx & Penelope Davis  |
*|                                                                    |
*|  Address questions to: mmarx@mdpaladin.com                         |
*|                                                                    |
*|  Permission is hereby granted, free of charge, to any person       |
*|  obtaining a copy of this software and associated documentation    |
*|  files (the "Software"), to deal in the Software without           |
*|  restriction, including without limitation the rights to use,      |
*|  copy, modify, merge, publish, distribute, sublicense, and/or sell |
*|  copies of the Software, and to permit persons to whom the         |
*|  Software is furnished to do so, subject to the following          |
*|  conditions:                                                       |
*|                                                                    |
*|  The above copyright notice and this permission notice shall be    |
*|  included in all copies or substantial portions of the Software.   |
*|                                                                    |
*|  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,   |
*|  EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES   |
*|  OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND          |
*|  NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT       |
*|  HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,      |
*|  WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING      |
*|  FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR     |
*|  OTHER DEALINGS IN THE SOFTWARE.                                   |
*+--------------------------------------------------------------------+
         MODULE DSPUT,BASE=12,LOC=BELOW,AMODE=31,RMODE=ANY,            X  23
               TEXT='MOVE DATA INTO DATASPACES'                           24
*        *---------------------------------------*                        25
*        *     CALL DSPFIND TO BUILD ACCESS      *                        26
*        *     LIST ENTRY FOR DSP1 DATASPACE     *                        27
*        *---------------------------------------*                        28
         LA    R3,DSPPARMS             PARMLIST FOR FIND SERVICE          29
         USING DSPTBLE,R3              MAP PARMLIST                       30
         OI    DSP_FUNCTION,@GETDUAL   SET FLAG TO BUILD DU-AL ENTRY      31
*                                      DSP1 OWNER NAME TO PARMLIST        32
         MVC   DSP_OWNER_NAME(L'DSP_OWNER_NAME),=CL8'DSPACE'              33
         MVC   DSP_XNAME(L'DSP_XNAME),DSNAME1                             34
         MODESET MODE=SUP              GET A SUPERVISOR STATE PSW         35
         LR    R1,R3                   PARMLIST ADDRESS TO R1             36
         ICM   R15,B'1111',=V(DSPFIND) A(DSPFIND SERVICE)                 37
         BZ    EXIT08                  =0; SERVICE NOT LINK-EDITED        38
         O     R15,=A(X'80000000')     SET FOR 31 BIT ENTRY               39
         BASSM R14,R15                 INVOKE SERVICE                     40
         LTR   R15,R15                 RC=0?                              41
         BNZ   EXIT10                  NO; EXIT                           42
         MVC   ALET1,DSP_ALET          ALET FOR DSP1                      43
         MVC   ORIGIN1,DSP_XORIGIN     ORIGIN FOR DSP1                    44
*        *---------------------------------------*                        45
*        *     CALL DSPFIND TO BUILD ACCESS      *                        46
*        *     LIST ENTRY FOR DSP2 DATASPACE     *                        47
*        *---------------------------------------*                        48
*                                      DSP2 OWNER NAME TO PARMLIST        49
         MVC   DSP_OWNER_NAME(L'DSP_OWNER_NAME),=CL8'DSPACE'              50
         MVC   DSP_XNAME(L'DSP_XNAME),DSNAME2                             51
         LR    R1,R3                   PARMLIST ADDRESS TO R1             52
         ICM   R15,B'1111',=V(DSPFIND) A(DSPFIND SERVICE)                 53
         O     R15,=A(X'80000000')     SET FOR 31 BIT ENTRY               54
         BASSM R14,R15                 INVOKE SERVICE                     55
         LTR   R15,R15                 RC=0?                              56
         BNZ   EXIT10                  NO; EXIT                           57
         MODESET MODE=PROB             GET A PROBLEM STATE PSW            58
         MVC   ALET2,DSP_ALET          ALET FOR DSP2                      59
         MVC   ORIGIN2,DSP_XORIGIN     ORIGIN FOR DSP2                    60
*        *---------------------------------------*                        61
*        *     ISSUE ENQ TO SERIALIZE ACCESS TO  *                        62
*        *     DATASPACE STORAGE                 *                        63
*        *---------------------------------------*                        64
         ENQ   (DSNAME1,DSRANGE,E,L'DSRANGE,SYSTEM)                       65
         ENQ   (DSNAME2,DSRANGE,E,L'DSRANGE,SYSTEM)                       66
*        *---------------------------------------*                        67
*        *     GO INTO ACCESS REGISTER MODE      *                        68
*        *---------------------------------------*                        69
         SAC   512                     GO INTO ACCESS REGISTER MODE       70
         LAE   R12,0(R12,0)            LOAD BASE AR WITH X'00'            71
         CPYA  A13,A12                 LOAD WORK AREA AR WITH X'00'       72
         SYSSTATE ASCENV=AR            SET FOR AR MACRO EXPANSIONS        73
*        *---------------------------------------*                        74
*        *     MOVE A WTO PARMLIST FROM THIS     *                        75
*        *     ADDRESS SPACE TO 1ST DATASPACE    *                        76
*        *     THEN FROM 1ST DATASPACE TO 2ND    *                        77
*        *     DATASPACE, THEN FROM 2ND DATASPC  *                        78
*        *     BACK TO THIS ADDRESS SPACE.       *                        79
*        *---------------------------------------*                        80
         LAM   A6,A6,ALET1             LOAD ACCESS REG W/DSP1 ALET        81
         L     R6,ORIGIN1              LOAD GEN REG W/DSP1 ORIGIN         82
         LAM   A7,A7,ALET2             LOAD ACCESS REG W/DSP2 ALET  @01   83
         L     R7,ORIGIN2              LOAD GEN REG W/DSP2 ORIGIN         84
         MVC   0(LWTOLIST,R6),WTOLIST  MOVE WTO PARMLIST TO DSP1          85
         MVC   0(LWTOLIST,R7),0(R6)    MOVE FROM DSP1 TO DSP2             86
         MVC   WTOPARM(LWTOLIST),0(7)  MOVE FROM DSP2 TO THIS SPACE       87
*        *---------------------------------------*                        88
*        *     GO INTO PRIMARY SPACE MODE        *                        89
*        *---------------------------------------*                        90
         SAC   0                       GO INTO PRIMARY SPACE MODE         91
         SYSSTATE ASCENV=P             SET FOR PRIMARY SPC EXAPNSIONS     92
*        *---------------------------------------*                        93
*        *     ISSUE WTO AGAINST PARMLIST THAT   *                        94
*        *     WAS MOVED AROUND                  *                        95
*        *---------------------------------------*                        96
         WTO   ,MF=(E,WTOPARM)         ISSUE WTO                          97
         B     EXIT00                                                     98
*---------------------------------------------------------------------*   99
*        EXIT ROUTINES                                                *  100
*---------------------------------------------------------------------*  101
EXIT00   DS    0H                      SUCCESSFUL                        102
         LA    15,X'00'                                                  103
         B     EXIT                                                      104
EXIT08   DS    0H                      DSPFIND NOT LINK-EDITED           105
         LA    15,X'08'                                                  106
         B     EXIT                                                      107
EXIT10   DS    0H                      DSPFIND DID NOT FIND DATASPACE    108
         PERCRC RC=X'10'                                                 109
         B     EXIT                                                      110
*---------------------------------------------------------------------*  111
*        COMMON EXIT                                                  *  112
*---------------------------------------------------------------------*  113
EXIT     DS    0H                                                        114
         LR    R2,R15                  PRESERVE RETURN CODE IN R2        115
*        *---------------------------------------*                       116
*        *     DEQ DATASPACE STORAGE             *                       117
*        *---------------------------------------*                       118
         DEQ   (DSNAME1,DSRANGE,L'DSRANGE,SYSTEM),RET=HAVE               119
         DEQ   (DSNAME2,DSRANGE,L'DSRANGE,SYSTEM),RET=HAVE               120
         LR    R15,R2                  RESTORE RETURN CODE TO R15        121
         ENDMOD                                                          122
*---------------------------------------------------------------------*  123
*        CONSTANTS                                                    *  124
*---------------------------------------------------------------------*  125
WTOLIST  WTO   'WTO PARMLIST MOVED: HOME => DSP1 => DSP2 => HOME',MF=L   126
LWTOLIST EQU   *-WTOLIST                                                 127
DSNAME1  DC    CL(L'DSP_XNAME)'DSP1'                                     128
DSNAME2  DC    CL(L'DSP_XNAME)'DSP2'                                     129
DSRANGE  DC    C'LOW100'                                                 130
*---------------------------------------------------------------------*  131
*        WORK AREA                                                    *  132
*---------------------------------------------------------------------*  133
WDSPUT   DSECT                                                           134
DSPPARMS DS    0F,CL(LDSPTBLE)         PARMLIST FOR DSPFIND SERVICE      135
ALET1    DS    F                       ALET FOR DSP1                     136
ORIGIN1  DS    F                       ORIGIN FOR DSP1                   137
ALET2    DS    F                       ALET FOR DSP2                     138
ORIGIN2  DS    F                       ORIGIN FOR DSP2                   139
WTOPARM  DS    0F,CL(LWTOLIST)         WTO PARMLIST                      140
LDSPUT   EQU   *-WDSPUT                                                  141
*---------------------------------------------------------------------*  142
*        OTHER DSECTS                                                 *  143
*---------------------------------------------------------------------*  144
         DSPTBLE                       MAP DSPTBLE BLOCK                 145
         END                                                             146
