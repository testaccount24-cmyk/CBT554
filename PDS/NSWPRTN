*+--------------------------------------------------------------------+
*|                      MAINTENANCE LOG                               |
*+----------+-------------------------------------+---------+---------+
*|  DATE    |       DESCRIPTION                   | CHANGED | REFER   |
*+----------+-------------------------------------+---------+---------+
*| 01/04/92 | GET KEY=8 STORAGE FOR SYSEVENT ECB  | MJM     | @01     |
*|          |   (FIX#-R0005)                      |         |         |
*+----------+-------------------------------------+---------+---------+
*                                                                     |
*+--------------------------------------------------------------------+    1
*| FUNCTION:                                                          |    2
*|   THIS SRB ROUTINE ISSUES SYSEVENT TRANSWAP SO THAT THE ADDRESS    |    3
*|   SPACE IN WHICH IT EXECUTES BECOMES NON-SWAPPABLE.                |    4
*| INPUTS:                                                            |    5
*|   R0 - ADDRESS OF THE SRB CONTROL BLOCK                            |    6
*|   R1 - ADDRESS OF THE SRB PARM LIST                                |    7
*| OUTPUTS:                                                           |    8
*|   THIS ROUTINE INDICATES ITS COMPLETION STATUS TO THE SCHEDULING   |    9
*|   TASK WITH A POST CODE IN THE SCHEDULER'S ECB.                    |   10
*| LOGIC:                                                             |   11
*|   1) ESTABLISH THE FRR RECOVERY ROUTINE                            |   12
*|   2) ISSUE SYSEVENT TO MAKE THE ADDRESS SPACE NON-SWAPPABLE        |   13
*|   3) CANCEL THE FRR                                                |   14
*|   4) CROSS-MEMORY POST THE SCHEDULER'S ECB.                        |   15
*| ATTRIBUTES:                                                        |   16
*|   AMODE 31, RMODE ANY, REENTRANT, ESA DEPENDENCIES                 |   17
*+--------------------------------------------------------------------+   18
*+--------------------------------------------------------------------+
*|  Copyright (c) 1992, 1993, 2002 by Mitchell Marx & Penelope Davis  |
*|                                                                    |
*|  Address questions to: mmarx@mdpaladin.com                         |
*|                                                                    |
*|  Permission is hereby granted, free of charge, to any person       |
*|  obtaining a copy of this software and associated documentation    |
*|  files (the "Software"), to deal in the Software without           |
*|  restriction, including without limitation the rights to use,      |
*|  copy, modify, merge, publish, distribute, sublicense, and/or sell |
*|  copies of the Software, and to permit persons to whom the         |
*|  Software is furnished to do so, subject to the following          |
*|  conditions:                                                       |
*|                                                                    |
*|  The above copyright notice and this permission notice shall be    |
*|  included in all copies or substantial portions of the Software.   |
*|                                                                    |
*|  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,   |
*|  EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES   |
*|  OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND          |
*|  NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT       |
*|  HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,      |
*|  WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING      |
*|  FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR     |
*|  OTHER DEALINGS IN THE SOFTWARE.                                   |
*+--------------------------------------------------------------------+
         MODULE NSWPRTN,BASE=9,LOC=BELOW,AMODE=31,RMODE=ANY,           X  19
               ENTRY=SRB,                                              X  20
               TEXT='SRB ROUTINE ISSUE SYSEVENT'                          21
         LR    R10,R0                  ADDRESS OF THE SRB                 22
         USING SRBSECT,R10             MAP SRB                            23
         XC    POSTCC(L'POSTCC),POSTCC SET POST CODE TO X'00'             24
*        *---------------------------------------*                        25
*        *     ESTABLISH FRR RECOVERY            *                        26
*        *---------------------------------------*                        27
         LA    R2,SRBFRRM              ADDRESS FRR ROUTINE                28
         SETFRR A,FRRAD=(R2),WRKREGS=(R4,R5),PARMAD=(R6)                  29
         LA    R1,RETRY                ..PUT RETRY ADDRESS                30
         ST    R1,0(R6)                ..INTO FRR PARAMETER               31
         ST    R10,4(R6)               SRB ADDRESS IN FRR PARMLIST        32
*        *---------------------------------------*                        33
*        *     MAKE ADDRESS SPACE NON-SWAPABLE   *                        34
*        *---------------------------------------*                        35
         STORAGE OBTAIN,COND=NO,LENGTH=4,KEY=8,SP=1,ADDR=TRANSECB   @01   350001
         L     R1,TRANSECB               ..MAKE THIS                @01   36
         SYSEVENT TRANSWAP,ENTRY=BRANCH  ..ADDRESS SPACE NON-SWAP         37
         STORAGE RELEASE,COND=NO,LENGTH=4,SP=1,ADDR=TRANSECB        @01   370001
*        *---------------------------------------*                        38
*        *     CANCEL THE FRR                    *                        39
*        *---------------------------------------*                        40
RETRY    DS    0H                      FRR RETRIES TO HERE                41
         SETFRR D,WRKREGS=(R4,R5)      DELETE THE FRR                     42
*        *---------------------------------------*                        43
*        *     CROSS-MEMORY POST THE SCHEDULER   *                        44
*        *---------------------------------------*                        45
         LA    R14,PRADD1              ADDRESS AFTER XMPOST               46
         BAKR  R14,R0                  CREATE STACK-STATE ENTRY           47
         LA    R4,SRBECB               ADDRESS OF SRB ECB                 48
         L     R5,ADASCB               ADDRESS OF SCHEDULER'S ASCB        49
         LA    R6,POSTERR              ERROR ROUTINE FOR POST             50
         L     R7,POSTCC               POST CODE                          51
         POST  (4),(7),ASCB=(5),ERRET=(6),LINKAGE=BRANCH                  52
         PR                            POP THE LINKAGE STACK              53
PRADD1   DS    0H                                                         54
         B     SRBX00                  EXIT                               55
*        *---------------------------------------*                        56
*        *     POST ERROR ROUTINE. ONLY ENTERED  *                        57
*        *     IF SCHEDULING ADDR. SPC. IS GONE  *                        58
*        *---------------------------------------*                        59
POSTERR  DS    0F                      POP THE STACK AND RETURN           60
         PR                                                               61
*---------------------------------------------------------------------*   62
*        FRR ROUTINE.                                                 *   63
*        THIS ROUTINE RECEIVES CONTROL IF THERE IS A PROGRAM CHECK    *   64
*        IN THE SRB ROUTINE.                                          *   65
*        - IF THE FRR IS NOT ENTERED RECURSIVELY IT ISSUES SETRP RC=4 *   66
*          SO THAT RTM1 TRANSFERS CONTROL TO THE SRB ROUTINE AT THE   *   67
*          RETRY ADDRESS (SUPPLIED IN THE FRR PARAMETER LIST).        *   68
*        - IF THE FRR IS ENTERED RECURSIVELY OR THE FRR ROUTINE       *   69
*          ITSELF ABENDS, RTM1 PERCOLATES THE ABEND TO THE TASK THAT  *   70
*          SCHEDULED THE SRB (SRB-TO-TASK PERCOLATION).               *   71
*---------------------------------------------------------------------*   72
SRBFRRM  DS    0F                                                         73
         BAKR  R14,R0                  SAVE REGISTERS ON LINKAGE STACK    74
         LR    R9,R15                  ENTRYPOINT TO BASE REGISTER        75
         LA    R2,SRBFRRM-NSWPRTN      ..BACK UP FOR DISPLACEMENT         76
         SLR   R9,R2                   ..OF THIS ROUTINE                  77
         LR    R13,R0                  WORK AREA ADDRESS                  78
         LR    R3,R1                   SDWA ADDRESS                       79
         USING SDWA,R3                 MAP SDWA                           80
         L     R6,SDWAABCC             ABEND CODE FROM SDWA               81
         SLL   R6,8                    CLEAR HIGH-ORDER BYTE              82
         SRL   R6,20                   ABEND CODE IN LOW ORDER BYTES      83
         ST    R6,POSTCC               SAVE AS POST CODE                  84
         L     R4,SDWAPARM             ADDRESS OF PARMLIST IN SDWA        85
         L     R10,4(R4)               ADDRESS OF SRB                     86
         TM    FRRFLAG,@RECUR          RECURSIVE ENTRY ?                  87
         BO    FRRABEND                ABEND                              88
         OI    FRRFLAG,@RECUR          RECURSIVE ENTRY ?                  89
         L     R5,0(R4)                RETRY ADDRESS                      90
         LR    R1,R3                   SDWA ADDRESS TO REG 1              91
         SETRP RC=4,RETADDR=(R5),RETREGS=YES,RETRY=FRR                    92
         PR                            POP THE STACK AND RETURN TO RTM    93
FRRABEND DS    0H                                                         94
         LR    R1,R3                   SDWA ADDRESS TO REG 1              95
         SETRP RC=0,COMPCOD=(X'200',USER),DUMP=NO                         96
         PR                            POP THE STACK AND RETURN TO RTM    97
*---------------------------------------------------------------------*   98
*        EXIT ROUTINES                                                *   99
*---------------------------------------------------------------------*  100
SRBX00   DS    0H                      ONLY EXIT FOR SRB ROUTINE         101
         LA    15,X'00'                ERROR CONDITIONS PASSED THRU      102
         B     SRBEXIT                 POST CODE                         103
*---------------------------------------------------------------------*  104
*        COMMON EXIT                                                  *  105
*---------------------------------------------------------------------*  106
SRBEXIT  DS   0H                                                         107
         ENDMOD                                                          108
*---------------------------------------------------------------------*  109
*        WORK AREA                                                    *  110
*---------------------------------------------------------------------*  111
WNSWPRTN DSECT                                                           112
TRANSECB DS    F                       ECB FOR SYSEVENT                  113
LNSWPRTN EQU   *-WNSWPRTN              LENGTH OF THE WORK AREA           114
*---------------------------------------------------------------------*  115
*        OTHER DSECTS                                                 *  116
*---------------------------------------------------------------------*  117
         SRBPRM                        PARMS FOR GETASCB                 118
         IHAFRRS                       MAP FRR                           119
         IHASDWA                       MAP SDWA                          120
         IHAPSA                        MAP PSA                           121
         CVT DSECT=YES,LIST=NO         MAP CVT                           122
         END                                                             123
