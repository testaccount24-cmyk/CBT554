         MACRO                                                             1
&LABEL   XTOC  &PLIST=,&ADOUT=,&LOUT=,&ADIN=,&LIN=                         2
.*+-------------------------------------------------------------------+
.*|  Copyright (c) 1992, 1993, 2002 by Mitchell Marx & Penelope Davis |
.*|                                                                   |
.*|  Address questions to: mmarx@mdpaladin.com                        |
.*|                                                                   |
.*|  Permission is hereby granted, free of charge, to any person      |
.*|  obtaining a copy of this software and associated documentation   |
.*|  files (the "Software"), to deal in the Software without          |
.*|  restriction, including without limitation the rights to use,     |
.*|  copy, modify, merge, publish, distribute, sublicense,and/or sell |
.*|  copies of the Software, and to permit persons to whom the        |
.*|  Software is furnished to do so, subject to the following         |
.*|  conditions:                                                      |
.*|                                                                   |
.*|  The above copyright notice and this permission notice shall be   |
.*|  included in all copies or substantial portions of the Software.  |
.*|                                                                   |
.*|  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,  |
.*|  EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES  |
.*|  OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND         |
.*|  NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT      |
.*|  HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,     |
.*|  WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING     |
.*|  FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR    |
.*|  OTHER DEALINGS IN THE SOFTWARE.                                  |
.*+-------------------------------------------------------------------+
.**********************************************************************    3
.*       GENERATES CALLING SEQUENCE FOR HEXPRT SERVICE                *    4
.*                  (CONVERT HEX TO PRINTABLE CHARACTERS)             *    5
.*       &PLIST  -  ADDRESS OF PARAMETER LIST                         *    6
.*       &ADOUT  -  ADDRESS OF OUTPUT STRING                          *    7
.*       &LOUT   -  LENGTH OF OUTPUT STRING                           *    8
.*       &ADIN   -  ADDRESS OF INPUT STRING                           *    9
.*       &LIN    -  LENGTH OF INPUT STRING                            *   10
.**********************************************************************   11
         LCLB  &E1                                                        12
         LCLC  &OP,&R,&LBL1,&LBL2                                         13
&LBL1    SETC  'PHX'.'&SYSNDX'                                            14
&LBL2    SETC  'PXX'.'&SYSNDX'                                            15
         AIF   (T'&PLIST NE 'O').OK1                                      16
         MNOTE 8,'OPERAND "PLIST" IS REQUIRED'                            17
&E1      SETB  1                                                          18
.OK1     ANOP                                                             19
         AIF   (T'&ADOUT NE 'O').OK2                                      20
         MNOTE 8,'OPERAND "ADOUT" IS REQUIRED'                            21
&E1      SETB  1                                                          22
.OK2     ANOP                                                             23
         AIF   (T'&LOUT NE 'O').OK3                                       24
         MNOTE 8,'OPERAND "LOUT" IS REQUIRED'                             25
&E1      SETB  1                                                          26
.OK3     ANOP                                                             27
         AIF   (T'&ADIN NE 'O').OK4                                       28
         MNOTE 8,'OPERAND "ADIN" IS REQUIRED'                             29
&E1      SETB  1                                                          30
.OK4     ANOP                                                             31
         AIF   (T'&LIN NE 'O').OK5                                        32
         MNOTE 8,'OPERAND "LIN" IS REQUIRED'                              33
&E1      SETB  1                                                          34
.OK5     ANOP                                                             35
         AIF   (&E1).MEND                                                 36
.*********************************************************************    37
.*       BUILD PARM LIST AND INVOKE SERVICE                          *    38
.*********************************************************************    39
         INNERMM &PLIST,1                                                 40
         INNERMM &ADOUT,15                                                41
         ST    15,0(1)               .. TO PLIST                          42
         INNERMM &LOUT,15                                                 43
         ST    15,4(1)               .. TO PLIST                          44
         INNERMM &ADIN,15                                                 45
         ST    15,8(1)               .. TO PLIST                          46
         INNERMM &LIN,15                                                  47
         ST    15,12(1)              .. TO PLIST                          48
         ICM   15,B'1111',=V(HEXPRT) .. ADDRESS OF OUTPUT STRING          49
         BNZ   &LBL1                 =0; ROUTINE IS NOT LINKED            50
         LA    15,13                 LOAD RETURN CODE = 13                51
         B     &LBL2                 DO NOT INVOKE ROUTINE                52
&LBL1    DS    0H                    INVOKE THE ROUTINE                   53
         O     15,=A(X'80000000')    SET 31BIT ENTRY                      54
         BASSM 14,15                 INVOKE THE ROUTINE                   55
&LBL2    DS    0H                                                         56
.MEND    MEND                                                             57
