         MACRO                                                             1
&LABEL   CALLPC &PLIST=                                                    2
.*+-------------------------------------------------------------------+
.*|  Copyright (c) 1992, 1993, 2002 by Mitchell Marx & Penelope Davis |
.*|                                                                   |
.*|  Address questions to: mmarx@mdpaladin.com                        |
.*|                                                                   |
.*|  Permission is hereby granted, free of charge, to any person      |
.*|  obtaining a copy of this software and associated documentation   |
.*|  files (the "Software"), to deal in the Software without          |
.*|  restriction, including without limitation the rights to use,     |
.*|  copy, modify, merge, publish, distribute, sublicense,and/or sell |
.*|  copies of the Software, and to permit persons to whom the        |
.*|  Software is furnished to do so, subject to the following         |
.*|  conditions:                                                      |
.*|                                                                   |
.*|  The above copyright notice and this permission notice shall be   |
.*|  included in all copies or substantial portions of the Software.  |
.*|                                                                   |
.*|  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,  |
.*|  EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES  |
.*|  OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND         |
.*|  NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT      |
.*|  HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,     |
.*|  WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING     |
.*|  FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR    |
.*|  OTHER DEALINGS IN THE SOFTWARE.                                  |
.*+-------------------------------------------------------------------+
         LCLB   &E1                                                        3
         LCLC   &LBL1,&LBL2,&LBL3,&LBL4,&LBL5                       #01    4
.*+-------------------------------------------------------------------+
.*|                     MAINTENANCE LOG                               |
.*+----------+------------------------------------+---------+---------+
.*| DATE     |       DESCRIPTION                  | CHANGED | REFER   |
.*+----------+------------------------------------+---------+---------+
.*| 04/17/92 | ADD LCLC DECLARATION FOR LBL5      | MJM     | #01     |
.*| 04/17/92 | SET AMODE BIT IN RETURN ADDRESS    | MJM     | #02     |
.*|          |   THAT WILL GO INTO STACK PSW      |         |         |
.*|          |   (FIX#-R0002)                     |         |         |
.*+----------+------------------------------------+---------+---------+
.*
.*+-------------------------------------------------------------------+    5
.*|      &PLIST IS THE ADDRESS OF A REMOTE PARAMETER LIST             |    6
.*|             MAPPED BY THE XMSESA MACRO                            |    7
.*+-------------------------------------------------------------------+    8
&LBL1    SETC  'CP1'.'&SYSNDX'                                             9
&LBL2    SETC  'CP2'.'&SYSNDX'                                            10
&LBL3    SETC  'CP3'.'&SYSNDX'                                            11
&LBL4    SETC  'CP4'.'&SYSNDX'                                            12
&LBL5    SETC  'CP5'.'&SYSNDX'                                            13
         AIF   (T'&PLIST NE 'O').OK1                                      14
         MNOTE 8,'OPERAND "PLIST=" IS REQUIRED'                           15
&E1      SETB  1                                                          16
.OK1     ANOP                                                             17
         AIF   (&E1).MEND                                                 18
.*+-------------------------------------------------------------------+   19
.*|                                                                   |   20
.*+-------------------------------------------------------------------+   21
&LABEL   LAE   14,&LBL5                ADDRESS AFTER STACK IS POPPED      22
         BSM   14,0                    SET AMODE IN RETURN REGISTER #02   22001
         BAKR  14,0                    SAVE REGISTERS AND ASC MODE        23
         SAC   0                       GO INTO PRIMARY ASC MODE           24
         L     3,CVTPTR                ADDRESS OF CVT                     25
         LA    9,&LBL2                 ADDRESS OF ANCHOR CDE NAME         26
         L     8,CVTQLPAQ-CVT(3)       HEAD OF LPAQ                       27
         L     15,CVTQCDSR-CVT(3)      ADDRESS OF THE LPAQ SCAN RTN       28
         BALR  14,15                   INVOKE LPAQ SCAN ROUTINE           29
         B     &LBL1                   CDE FOUND                          30
         LA    15,8                    CDE NOT FOUND; SET RC=8            31
         B     &LBL4                   BRANCH TO POP THE STACK            32
&LBL1    L     11,CDENTPT-CDENTRY(11)  POINTER TO PC # TABLE IN CDE       33
         L     14,PCSRTN1-PCSERV(11)   PC NUMBER FOR "ADD"                34
         INNERMM &PLIST,1              PARSE OPERAND                      35
         B     &LBL3                   ISSUE PC INSTRUCTION               36
&LBL2    DC    CL8'PCTBL01'            NAME OF CDE THAT ANCHORS PCTBL     37
&LBL3    PC    0(14)                   INVOKE THE PC ROUTINE              38
&LBL4    PR                            POP THE STACK                      39
&LBL5    DS    0H                                                         40
.MEND    MEND                                                             41
